Section Header

  + name        := EXPR_EQUAL;

  - copyright   := "2003-2007 Benoit Sonntag";


  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "Simple Equal Binary Expression.";

Section Inherit

  + parent_expr_binary_cmp:Expanded EXPR_BINARY_CMP;

Section Public

  - symbol:STRING_CONSTANT := "==";

  //
  // Execute.
  //

  - exec left_cst:INTEGER_CST and right_cst:INTEGER_CST :EXPR <-
  //-- C1 == C2 -> TRUE/FALSE
  ( + result:PROTOTYPE_CST;

    (left_cst.value = right_cst.value).if {
      result := PROTOTYPE_CST.create position type (type_true.default);
    } else {
      result := PROTOTYPE_CST.create position type (type_false.default);
    };
    left_cst .remove;
    right_cst.remove;

    count_equal_const := count_equal_const + 1;

    result
  );

  - exec:EXPR <-
  //-- E == E -> TRUE
  //-- Expanded == NULL -> FALSE
  //-- intersection (type_set(E),type_set(E')) = Empty -> FALSE
  ( + result:PROTOTYPE_CST;
    + r:PROTOTYPE_CST;
    + type_set_r,type_set_l:TYPES_TMP;

    (left ~= right).if {
      result := PROTOTYPE_CST.create position type (type_true.default);
      left .remove;
      right.remove;

      (left.static_type.raw = TYPE_NULL).if {
        count_equal_null := count_equal_null + 1;
      } else {
        count_equal_ident := count_equal_ident + 1;
      };

    } else {
      r ?= right;
      (
	(r != NULL) &&
	{r.static_type.raw = TYPE_NULL} &&
	{left.static_type.is_expanded}  &&
	{left.static_type.raw != type_pointer}
      ).if {
        left .remove;
        right.remove;
        result := PROTOTYPE_CST.create position type (type_false.default);

        count_equal_const := count_equal_const + 1;

      }.elseif {(! left.static_type.is_expanded) && {! right.static_type.is_expanded}} then {
        type_set_l := TYPES_TMP.new;
        left.get_type type_set_l;
        type_set_r := TYPES_TMP.new;
        right.get_type type_set_r;
        (
          (type_set_l.count = 1)         && {type_set_r.count = 1} &&
          {type_set_l.first = TYPE_NULL} && {type_set_r.first = TYPE_NULL}
        ).if {
          left .remove;
          right.remove;
          result := PROTOTYPE_CST.create position type (type_true.default);

          count_equal_null := count_equal_null + 1;

        }.elseif {type_set_l.intersection_is_empty type_set_r} then {
          left .remove;
          right.remove;
          result := PROTOTYPE_CST.create position type (type_false.default);

          count_equal_inter := count_equal_inter + 1;

        };
        type_set_l.free;
        type_set_r.free;
      };
    };

    result
  );

  //
  // Genere
  //

  - genere buffer:STRING <-
  (

    count_equal_gener := count_equal_gener + 1;

    (
      (left.static_type.is_expanded)  &&
      {right.static_type.is_expanded} &&
      {right.static_type.raw.type_c = NULL}
    ).if {
      buffer.append "(memcmp(";
      (left.static_type.is_expanded_ref).if_false {
        buffer.add_last '&';
      };
      left.genere buffer;
      buffer.add_last ',';
      (right.static_type.is_expanded_ref).if_false {
        buffer.add_last '&';
      };
      right.genere buffer;
      buffer.append ",sizeof(";
      left.static_type.raw.put_expanded_declaration buffer;
      buffer.append ")) == 0)";
    } else {
      parent_expr_binary_cmp.genere buffer;
    };
  );






