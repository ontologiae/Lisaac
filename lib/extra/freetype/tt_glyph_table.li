Section Header

  + name        := TT_GLYPH_TABLE;
  - comment     :=" FreeType Glyph Object Type.";

  - bibliography:="http://IsaacOS.com";
  - author      :="Boutet Jerome (boutet@loria.fr)";

Section Inherit

  - parent:OBJECT := OBJECT;

Section Public

  + glyph:FAST_ARRAY(TT_GLYPH);

  - create loca:TT_LOCA my_file file:FILE :SELF <-
  ( + result:SELF;
    ? { loca != NULL};
    ? { file != NULL};
    result := SELF.clone;
    result.make loca my_file file;
    result
  );

  - make loca:TT_LOCA my_file file:FILE <-
  ( + cursor:UINTEGER_32;
    cursor := file.cursor;
    glyph := FAST_ARRAY(TT_GLYPH).create_with_capacity (loca.max_loca - 1);
    0.to (loca.max_loca-2) do { i:INTEGER;
      (loca.value (i + 1) - loca.value i > 0).if {
	file.set_cursor (cursor + loca.value i);
	glyph.add_last (TT_GLYPH.create file);
      } else {
	glyph.add_last NULL;
      };
    };
  );

  - print <-
  (
    "\n********* GLYPH TABLE*********\n".print;
    0.to (glyph.upper) do { i:INTEGER;
      "\nGlyph #".print;
      i.print;
      '\n'.print;
      (glyph.item i = NULL).if {
        "Empty".print;
      } else {
        glyph.item i.print;
      };
      '\n'.print;
    };
    '\n'.print;
  );

