///////////////////////////////////////////////////////////////////////////////
//                             Lisaac Compiler                               //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////
Section Header
  
  + name      := LIP_AFFECT;

  - copyright := "2003-2008 Sonntag Benoit";

  - author    := "Sonntag Benoit (sonntag@icps.u-strasbg.fr)";
  - comment   := "The main prototype";

Section Inherit

  + parent_lip_code:Expanded LIP_CODE;

Section Public
  
  + name:STRING_CONSTANT;
  
  + value:LIP_CODE;
  
  //
  // Creation.
  //

  - create p:POSITION name n:STRING_CONSTANT value val:LIP_CODE :SELF <-
  ( + result:SELF;
    result := clone;
    result.make p name n value val;
    result
  );

  - make p:POSITION name n:STRING_CONSTANT value val:LIP_CODE <-
  ( 
    position := p;
    name := n;
    value := val;
  );
  
  //
  // Run.
  //
  
  - run <-
  ( + slot:LIP_SLOT_DATA;
    + val:LIP_CONSTANT;
    
    slot := get_data name;
    (slot = NULL).if {
      string_tmp.copy "Slot `";
      string_tmp.append name;
      string_tmp.append "' not found.";
      semantic_error (position,string_tmp);
    };
    val := value.run_expr;
    (slot.set_value val).if_false {
      semantic_error (position,"Incorrect type.");
    };
    val.free;
  );