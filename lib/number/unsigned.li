/***************************************************************************
*                      Isaac Object Operating System                       *
*                             Lisaac Compiler                              *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
***************************************************************************/

section HEADER
  
  - name        :=UNSIGNED;
  
  - category    :=MICRO;
  
  - bibliography:="http://IsaacOS.com";
  - author      :="Sonntag Benoit (bsonntag@loria.fr)";
  - comment     :="Generic Unsigned Integer.";
  
section INHERIT
  
  - parent_numeric:NUMERIC := NUMERIC;
  
section PUBLIC
  
  //
  // Range
  //
  
  - minimum:LONGINT := 0;
  
  //
  // Function :
  //
  
  - sign:INTEGER <-
  // Sign of Current (0 -1 or 1).
  ( + result:INTEGER;
    
    result:=(self!=0).to_integer;
    
    ? {0<=result};
    ? {result<=1};
    result
  );
  
  - abs:SELF <- self;	// Absolute value of `self'.
  
  //
  // Convertion
  //
  
  - append_in buffer:STRING <-
  // Append in the `buffer' the equivalent of `to_string'. No new STRING
  // creation during the process.
  ( + val:SELF;
    + i,j:INTEGER;
    ? {buffer!=NULL};
    
    (self == 0).if {
      buffer.extend '0';
    } else {
      
      i := buffer.upper+1;
      
      val := self;
      {val == 0}.until_do {
	buffer.extend ((val % 10).digit);
	val := val / 10;
      };
      j := buffer.upper;
            
      {i >= j}.until_do {
	buffer.swap i with j;
	j := j - 1;
	i := i + 1;
      };

    };
  );
  
  - to_octal:SELF <-
  // Gives coresponding octal value.
  ( + result, unit, current:SELF;
    
    current := self;
    unit := 1;
    {current!=0}.while_do {
      result := result + ((current & 7)*unit);
      unit := (unit << 3) + (unit << 1);
      current := current >> 3;
    };
    result
  );
  
  //
  // Hashing:
  //
  
  - hash_code:UINT <- self;
