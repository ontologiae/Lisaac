Section Header

  + name        := ITM_WRITE_CODE;

  - copyright   := "2003-2007 Benoit Sonntag";


  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "Assignment slot with new code";

Section Inherit

  + parent_itm_write:Expanded ITM_WRITE;

Section Public

  - type:STRING_CONSTANT <- "<-";

  //
  // Runnable
  //

  - to_run_expr:EXPR <-
  ( + itm_read:ITM_READ;
    + loc:LOCAL;
    + rec:EXPR;
    + node:NODE;
    + result:EXPR;
    + type:TYPE_FULL;
    + slot:SLOT;
    + name:STRING_CONSTANT;

    itm_read ?= assign;
    name := itm_read.name;
    ? {itm_read != NULL};
    loc := lookup (ALIAS_STR.variable_self);
    rec := loc.read position;
    //
    type     := rec.static_type;
    slot     := type.get_slot name;
    (slot = NULL).if {
      string_tmp.copy "Slot `";
      string_tmp.append name;
      string_tmp.append "' not found in static type ";
      type.append_name_in string_tmp;
      string_tmp.add_last '.';
      semantic_error (position,string_tmp);
    };
    //
    node := NODE.new_write position slot slot receiver rec code value;
    list_current.add_last node;
    result := node.result_expr;

    result
  );





