/****************************************************************************
*                     Isaac Object Operating System                        *
*                             Lisaac Compiler                              *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Jerome BOUTET  - boutet@loria.fr                       *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/

section HEADER
  
  + name        := INSTALL_LISAAC;

  - comment     :="Configure the system file and the PATH";
  
  - category    := MACRO;
  
  - bibliography:="http://IsaacOS.com";
  - author      :="Sonntag Benoit (bsonntag@loria.fr) - Boutet Jerome (boutet@loria.fr)";
  
section INHERIT
  
  - parent_object:OBJECT <- OBJECT;
  
section PRIVATE  
  
  - update file:ABSTRACT_STRING idf id:STRING_CONSTANT with new_text:ABSTRACT_STRING <-
  ( + index:INTEGER;
    + old_buffer,input:STRING;
    + std_file:STD_FILE;
    
    "  A `".print;
    file.print;
    
    std_file := STD_FILE.create_open file;
    (std_file.is_open).if {
      // Update file.
      input := STRING.create (std_file.size + new_text.count);
      std_file.read input size (std_file.size);
      std_file.close;
      //    
      index := input.first_substring_index id;
      (index != 0).if {
	// Update configuration.
	old_buffer := STRING.create 200;
	1.to (new_text.occurrences '\n') do { j:INTEGER; 
	  {input.item index != '\n'}.while_do {
	    old_buffer.add_last (input.item index);
	    input.remove index;
	  };
	  old_buffer.add_last (input.item index);
	  input.remove index;
	};
	(old_buffer == new_text).if {
	  "' file has no need to change. Current version is:\n".print;
	} else {
	  "' file has been updated. Old value is:\n".print;
	  old_buffer.print;
	  "  New value is:\n".print;
	};
      } else {
	"' file has been updated with:\n".print;
	input.add_last '\n';
	index := input.upper + 1;
      };
      new_text.print;
      // Append configuration.
      input.insert_string new_text to index;
    } else {
      // Creation file.
      "' file has been created with:\n".print; 
      new_text.print;
      input := new_text;
    };
      
    std_file := STD_FILE.create file;
    std_file.write input from (input.lower) size (input.count);
    std_file.close;      
  );
  
  //
  // Global variable.
  //
  
  - path_current:STRING;
  - path_home   :STRING;
  - shell        :STRING;
  - system      :STRING_CONSTANT;
  
  //
  // Constant for environment variable & path.
  //
 
  - system_unix_bash:STRING_CONSTANT := "Unix - bash";
  - system_unix_tcsh:STRING_CONSTANT := "Unix - tcsh";
  - system_windows  :STRING_CONSTANT := "Windows - DOS";
  - system_unknown  :STRING_CONSTANT := "Unknown";
  
  - file_bashrc  :STRING_CONSTANT := "/.bashrc";
  - file_cshrc   :STRING_CONSTANT := "/.cshrc";
  - file_autoexec:STRING_CONSTANT := "\\autoexec.bat";
  
  - comment_windows :STRING_CONSTANT := "REM **** LISAAC COMPILER ****\n";
  - comment_unix    :STRING_CONSTANT := "# **** LISAAC COMPILER ****\n";
  
  - lisaac_bash   :STRING_CONSTANT := "export LISAAC=";
  - lisaac_tcsh   :STRING_CONSTANT := "setenv LISAAC ";
  - lisaac_windows:STRING_CONSTANT := "set LISAAC=";
  
  - path_bash   :STRING_CONSTANT := "export PATH=";
  - path_tcsh   :STRING_CONSTANT := "set path=(";
  - path_windows:STRING_CONSTANT := "set path=";
  
  - path_bash_next   :STRING_CONSTANT := "/bin:$PATH";
  - path_tcsh_next   :STRING_CONSTANT := "/bin $path)";
  - path_windows_next:STRING_CONSTANT := "\\bin;%path%";
    
  //
  // Detect system and install environment variables.
  //
  
  - install_variable <-
  ( + std_file :STD_FILE;
    + new_text :STRING;
    + file     :STRING;
    + comment  :STRING_CONSTANT;
    + lisaac   :STRING_CONSTANT;
    + path     :STRING_CONSTANT;
    + path_next:STRING_CONSTANT;
        
    file := STRING.create_from_string path_home;
    
    //
    // Detect system
    //
    
    (shell.has_substring "bash").if {
      // Unix - bash
      file.append file_bashrc;
      system    := system_unix_bash;
      comment   := comment_unix; 
      lisaac    := lisaac_bash;
      path      := path_bash;
      path_next := path_bash_next;
    }.elseif { shell.has_substring "tcsh" } then {
      // Unix - tcsh
      file.append file_cshrc;
      system := system_unix_tcsh;
      comment   := comment_unix; 
      lisaac    := lisaac_tcsh;
      path      := path_tcsh;
      path_next := path_tcsh_next;
    } else {
      // On other shell
      std_file := STD_FILE.create_open file_autoexec;
      (std_file.is_open).if {
	// Windows
	std_file.close;
	file.append file_autoexec;
	system    := system_windows;
	comment   := comment_windows; 
	lisaac    := lisaac_windows;
	path      := path_windows;
	path_next := path_windows_next;
      } else {
	// Unknown
	system := system_unknown;
      };  
    };        
    
    "  System detect: ".print;
    system.print;
    '\n'.print;
    (system != system_unknown).if {
      (system = system_windows).if {
	update "path.li" idf "  + target" with "  + target := WINDOWS;\n";
      } else {
	update "path.li" idf "  + target" with "  + target := UNIX;\n";
      };
    };
    '\n'.print;
    
    //
    // Installation environment variable
    //
        
    "Step 2/4 : Installation of environment variables.\n\
    \=================================================\n".print;
    
    (system = system_unknown).if {
      // Fail.
      "  Auto-install fail !\n\
      \  You have to change your environment variables as following: \n\
      \    set LISAAC=".print;
      path_current.print;
      "\n    set path=".print;
      path_current.print;
      "\\bin;%path%\n\n".print;
    } else {
      // Creation environment variables.
      new_text := STRING.create_from_string comment;
      new_text.append lisaac;
      new_text.append path_current;
      new_text.add_last '\n';
      new_text.append path;
      new_text.append path_current;
      new_text.append path_next;
      new_text.append "\n\n";
      
      update file idf comment with new_text;
    };
  );
  
  //
  // Install for Emacs.
  //
  
  - lisaac_mode_comment :STRING_CONSTANT := ";; **** LISAAC MODE ****\n";
  - lisaac_mode_path    :STRING_CONSTANT := "(setq load-path (cons \"";
  - lisaac_mode_path_end:STRING_CONSTANT := "/emacs/\" load-path))\n"; 
  - lisaac_mode         :STRING_CONSTANT := 
  "(add-to-list 'auto-mode-alist '(\"\\\\.li\\\\'\" . lisaac-mode))\n\
  \(autoload 'lisaac-mode \"lisaac-mode\" \"Major mode for Lisaac Programs\" t)\n\n";

  - install_emacs <-
  ( + char:CHARACTER;
    + file_name, new_text:STRING;
    
    "  Do you want to install the `lisaac-mode' for Emacs editor ? (y/n)".print;
    {(char != 'y') && {char != 'n'}}.while_do {
      char := IO.read_character;
    };
    (char = 'n').if {
      "  Not install `lisaac-mode' for Emacs editor.\n\n".print;
    } else {
      file_name := STRING.create_from_string path_home;
      file_name.append "/.emacs";
      new_text := STRING.create_from_string lisaac_mode_comment;
      new_text.append lisaac_mode_path;
      new_text.append path_current;
      new_text.append lisaac_mode_path_end;
      new_text.append lisaac_mode;
      update file_name idf lisaac_mode_comment with new_text;
    };
  );
  
  //
  // Install Compiler
  //
  
  - install_compiler <-
  ( + cmd:STRING_CONSTANT;
    
    (system = system_windows).if {
      cmd := "gcc -O3 bin/lisaac.c -o bin/lisaac.exe";
    } else {
      cmd := "gcc -O3 bin/lisaac.c -o bin/lisaac";
    };
    "  Execute command `".print;
    cmd.print;
    "' (please wait ...)\n".print;
    (ENVIRONMENT.execute_command cmd != 0).if {
      "  Auto-install fail !\n\
      \  You want to compile a `bin/lisaac.c' file.\n".print;
    };
    '\n'.print;
  );
section PUBLIC
  
  + main <-
  (
    "\t\t================================\n\
    \\t\t= Auto-Install Lisaac Compiler =\n\ 
    \\t\t================================\n\n".print;
     
    path_current := DIRECTORY.get_current_working_directory;
    path_home    := ENVIRONMENT.get_environment_variable "HOME";
    shell        := ENVIRONMENT.get_environment_variable "SHELL";
    
    "Step 1/4 : Detection system.\n\
    \============================\n".print;    
    install_variable;

    "Step 3/4 : Installation of `lisaac-mode' for Emacs.\n\
    \===================================================\n".print;
    install_emacs;      
    
    "Step 4/4 : Compilation of Lisaac compiler.\n\
    \==========================================\n".print;
    install_compiler;
    
    "Welcome to the Lisaac World !\n\
    \=============================\n".print;
    
    "  Installation successfull.\n\
    \  Run `lisaac' to compile.\n\
    \  Note: You'll have to reboot or reloaded environnement\n\
    \        to acknowledge the changes.\n".print;
  );






