Section Header

  + name     := GL_PLANE;

  - comment  := "a*x+b*y+c*z+d >= 0 clipping plane";

Section Inherit

  + parent_plane:Expanded PLANE;

Section Public

  - nb_planes:INTEGER;

  + id:INTEGER; // OpenGL plane id

  - load_clipping_plane <-
  // called while plane creation
  (
    // OpenGl allow only 6 clipping planes at a time.
    (nb_planes >= 6).if {
      GL_ERROR.send "too much clipping planes";
    };
    id := `GL_CLIP_PLANE0`:INTEGER + nb_planes;
    nb_planes := nb_planes + 1;
  );

  - clip <-
  (
    + i:INTEGER;
    + p:POINTER;

    enable;

    i := id;
    `glEnable(@i)`;

    p := getv.to_external;
    `glClipPlane(@i, @p)`;
  );

  - push_attrib <-
  (
    `glPushAttrib(GL_ENABLE_BIT)`;
  );

  - pop_attrib <-
  (
    `glPopAttrib()`;
  );

  - unclip <-
  (
    + i:INTEGER;

    disable;

    i := id;
    `glDisable(@i)`;
  );
