Section Header

  + name      := AGENT;

  - copyright := "2003-2010 Sonntag Benoit";

  - author    := "Sonntag Benoit (sonntag@icps.u-strasbg.fr)";
  - comment   := "The main prototype";

Section Inherit

  - parent_object:OBJECT := OBJECT;

Section Public

  + birthtime:UINTEGER_64;

  + current_state:STATE;

  + state_time:UINTEGER_64;

  + behavior:STATE;

  - make s:STATE <-
  (
    behavior := s;
    set_state s;
    birthtime := TIMER.timer_count;
  );

  - set_state s:STATE <-
  [ -? {s != NULL}; ]
  ( + cur:STATE;
    (current_state != NULL).if {
      // Execute End action of `current_stat'.
      cur := current_state;
      {cur != NULL}.while_do {
        (cur.end_action != NULL).if {
          cur.end_action.value Self;
        };
        (s.parent_state != cur.parent_state).if {
          cur := cur.parent_state;
        } else {
          cur := NULL;
        };
      };
    };
    state_time := TIMER.timer_count;
    // Update State & Execute Begin action.
    cur := s;
    {
      current_state := cur;
      (current_state.begin_action != NULL).if {
        current_state.begin_action.value Self;
      };
      cur := current_state.sub_state;
    }.do_while {cur != NULL};
  );

  //
  // Timer.
  //

  - time_in_state:UINTEGER_64 <-
  (
    TIMER.timer_count - state_time
  );

  - lifetime:UINTEGER_64 <-
  (
    TIMER.timer_count - birth_time
  );

  //
  // Run.
  //

  - run <-
  (
    (current_state != NULL).if {
      current_state.run Self;
    };
  );
