///////////////////////////////////////////////////////////////////////////////
//                             Lisaac Compiler                               //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////
Section Header
  
  + name        := EXPR_SHIFT_L;

  - copyright   := "2003-2007 Benoit Sonntag";

  
  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "Add Binary Expression.";
  
Section Inherit
  
  + parent_expr_binary:Expanded EXPR_BINARY;
  
Section Public
  
  + symbol:STRING_CONSTANT := "<<";
  
  //
  // Execute.
  //
  
  - exec_conservator_right right_cst:INTEGER_CST :EXPR <- 
  //-- E <<  0 -> E  
  ( + result:EXPR;
    
    (right_cst.value = 0).if {
      result := left;
      right_cst.remove;
    };
    result
  );
  
  - exec left_cst:INTEGER_CST and right_cst:INTEGER_CST :EXPR <- 
  //-- C1 << C2  -> C3
  //-- C1 << -C2 -> Error.
  (
    (right_cst.value < 0).if {
      warning_error (position,"Left shift count is negative.");
      left_cst.set_value 0;
    } else {
      left_cst.set_value (left_cst.value << right_cst.value); 
    };
    right_cst.remove;
    left_cst
  );

  - exec_left  left_cst :INTEGER_CST :EXPR <- 
  //-- 0 << E   -> 0
  ( + result:EXPR;
    
    (left_cst.value = 0).if {
      result := left_cst;
      right.remove;
    };
    result
  );
  
  - exec_right right_cst:INTEGER_CST :EXPR <- 
  //-- E << -C2 -> Error.
  ( + result:EXPR;
    
    (right_cst.value < 0).if {
      warning_error (position,"Left shift count is negative.");
      right_cst.set_value 0;
      right_cst.cast_type static_type;
      result := right_cst;
      left.remove;
    };
    result
  );
  
  
