///////////////////////////////////////////////////////////////////////////////
//                              Lisaac Example                               //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////
Section Header
  
  + name         := PARTIAL_SUMS;
  
  - bibliography := "http://IsaacOS.com";
  - author       := "Xavier Oswald (x.oswald@free.fr)";
  - comment      := "Language shootout - partial-sums";
  
Section Inherit
  
  - parent_any:OBJECT := OBJECT;

Section Public
  
  - two_third :REAL_64 := 2.to_real_64/3.0;

  - main <-
  ( + n :INTEGER;
    + a1, a2, a3, a4, a5   :REAL_64;
    + a6, a7, a8, a9, alt  :REAL_64;
    //+ sk, ck       :REAL_64;
    + nr :REAL_64;
    //+ k2:INTEGER_64; 
    //+ k3,kr64:REAL_64;
    
    alt := 1.0;

    (COMMAND_LINE.upper = 1).if {
      n := COMMAND_LINE.item 1.to_integer;
      nr := n.to_real_64;
    };

    0.to_real_64.to (nr-1.0) do { k:REAL_64;
      a1 := a1 + two_third.pow k; 
    };
    1.to_real_64.to nr do { k:REAL_64;
      a2 := a2 + 1.to_real_64/k.sqrt;
    };
    1.to_real_64.to nr do { k:REAL_64;
      a3 := a3 + 1.to_real_64/(k * (k + 1.0));
    };            
    1.to_real_64.to nr do { k:REAL_64;      
      a4 := a4 + 1.to_real_64/(k.sin * k.sin*k*k*k);
    };            
    1.to_real_64.to nr do { k:REAL_64;      
      a5 := a5 + 1.to_real_64/(k.cos * k.cos*k*k*k);
    };        
    1.to n do { k:INTEGER;
      a6 := a6 + 1.to_real_64/k.to_real_64;
    };    
    1.to_real_64.to nr do { k:REAL_64;
      a7 := a7 + 1.to_real_64/(k * k);
    };
    
    alt := 1.0;
    1.to n do { k:INTEGER;
      a8 := a8 + alt/k.to_real_64;
      alt := -alt;
    };    
    
    alt := 1.0;
    1.to n do { k:INTEGER;
      a9 := a9 + alt/(k*2-1).to_real_64;
      alt := -alt;
    };
    
    a1.print_format_c "%.9f";
    "\t(2/3)^k\n".print;
    a2.print_format_c "%.9f";
    "\tk^-0.5\n".print;
    a3.print_format_c "%.9f";
    "\t1/k(k+1)\n".print;
    a4.print_format_c "%.9f";
    "\tFlint Hills\n".print;
    a5.print_format_c "%.9f";
    "\tCookson Hills\n".print;
    a6.print_format_c "%.9f";
    "\tHarmonic\n".print;
    a7.print_format_c "%.9f";
    "\tRiemann Zeta\n".print;
    a8.print_format_c "%.9f";
    "\tAlternating Harmonic\n".print;
    a9.print_format_c "%.9f";
    "\tGregory\n".print;
  );
