///////////////////////////////////////////////////////////////////////////////
//                             Lisaac Compiler                               //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////
Section Header
  
  + name        := ITEM;

  - copyright   := "2003-2007 Benoit Sonntag";

  
  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "Item for NATIVE_ARRAY[type].";
  
Section Inherit
  
  + parent_expr:Expanded EXPR;
  
Section Public 
  
  - is_invariant:BOOLEAN <- receiver.is_invariant && {index.is_invariant};
  
  + receiver:EXPR;
  
  + index:EXPR;
    
  - static_type:TYPE_FULL <- 
  ( + t:TYPE_GENERIC;
    
    t ?= receiver.static_type.raw;
    t.generic_list.first    
  );
     
  - get_type t:TYPES_TMP <-
  ( + typ_gen:TYPE_GENERIC;   
        
    typ_gen ?= receiver.static_type.raw;
    typ_gen.get_type t;    
  );
  
  //
  // Creation.
  //
  
  - create p:POSITION base rec:EXPR index idx:EXPR :SELF <-
  ( + result:SELF;
    
    result := clone;
    result.make p base rec index idx;
    result
  );
  
  - make p:POSITION base rec:EXPR index idx:EXPR <-
  (
    position := p;            
    receiver := rec;
    index    := idx;
  );
  
  - my_copy:SELF <- SELF.create position base (receiver.my_copy) index (index.my_copy);
  
  //
  // Remove
  //
  
  - remove <-
  (
    receiver.remove;
    index.remove;
  );
  
  //
  // Execute
  //
  
  - execute_unlink:INSTR <-
  ( + instr:INSTR;    
    instr := receiver.execute_unlink;
    (instr != NULL).if {
      list_current.insert_before instr;
    };
    index.execute_unlink    
  );
  
  - execute_link:EXPR <-
  (   
    receiver := receiver.execute_link;
    index    := index.execute_link;    
    Self
  );
    
  //
  // Genere.
  //
  
  - genere buffer:STRING <-
  (
    receiver.genere buffer;
    buffer.add_last '[';
    index.genere buffer;
    buffer.add_last ']';    
  );
  
  //
  // Display.
  //
  
  - display buffer:STRING <-
  (
    receiver.display buffer;
    buffer.add_last '[';
    index.display buffer;
    buffer.add_last ']';
  );









