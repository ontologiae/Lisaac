Section Header

  + name        := TT_DRAW;

  - comment     := "Draw TT Font";

  - bibliography:= "http://IsaacOS.com";
  - author      := "Benoit Sonntag (bsonntag@loria.fr), Jerome Boutet (boutet@loria.fr)";

Section Inherit

  - parent_object:OBJECT := OBJECT;
  
Section Private  
  
  - to_bezier (b_x,b_y,e_x,e_y:INTEGER_32) with (p_x,p_y:INTEGER_32) :(INTEGER_32,INTEGER_32,INTEGER_32,INTEGER_32) <-
  (
    b_x + 2*(p_x-b_x)/3,
    b_y + 2*(p_y-b_y)/3,
    p_x +   (e_x-p_x)/3,
    p_y +   (e_y-p_y)/3
  );

Section Public
  
  - is_debug:BOOLEAN := TRUE;
  
  - origin_x:INTEGER_32 := 64;
  - origin_y:INTEGER_32 := 370;
  
  - to_x x:INTEGER_32 :INTEGER_32 <-x/5 + origin_x;
  
  - to_y y:INTEGER_32 :INTEGER_32 <-origin_y - y/5;

  - make glyph:TT_GLYPH bitmap b:ABSTRACT_BITMAP <-
  ( + x,y,x_old,y_old,x_new,y_new,p0_x,p0_y,p1_x,p1_y,p2_x,p2_y,x_contour,y_contour:INTEGER_32;    
    + n_contour,step:INTEGER;
    + in,new_contour:BOOLEAN;
    + in_step,out_step:{};
    
    b.rectangle_fill (origin_x-1,origin_y-1) to (origin_x+1,origin_y+1) color (b.black);    
    b.rectangle (to_x (glyph.x_min),to_y (glyph.y_min)) to (to_x (glyph.x_max),to_y (glyph.y_max)) color (b.blue);    
    
    (is_debug).if {
      in_step := 
      {
        (step = 0).if {
          b.line (x_old,y_old) to (x,y) color (b.black); 
        } else {
          (p1_x,p1_y,p2_x,p2_y) := to_bezier (p0_x,p0_y,x,y) with (x_old,y_old);
          b.spline (p0_x,p0_y) w1 (p1_x,p1_y) w2 (p2_x,p2_y) to (x,y) color (b.purple); 
          step := 0;          
        };          
      };
      out_step := 
      {
        (step = 0).if {
          p0_x := x_old; p0_y := y_old; step := 1; 
        } else {
          x_new := (x_old + x) / 2;
          y_new := (y_old + y) / 2;
          (p1_x,p1_y,p2_x,p2_y) := to_bezier (p0_x,p0_y,x,y) with (x_old,y_old);
          b.spline (p0_x,p0_y) w1 (p1_x,p1_y) w2 (p2_x,p2_y) to (x_new,y_new) color (b.purple); 
          p0_x := x_new;
          p0_y := y_new;
        };
      };
    } else {
      in_step := 
      {
        (step = 0).if {
          b.poly_line_to (x,y);
        } else {
          (p1_x,p1_y,p2_x,p2_y) := to_bezier (p0_x,p0_y,x,y) with (x_old,y_old);
          b.poly_spline_w1 (p1_x,p1_y) w2 (p2_x,p2_y) to (x,y); 
          step := 0;          
        };          
      };
      out_step := 
      {
        (step = 0).if {
          p0_x := x_old; p0_y := y_old; step := 1; 
        } else {
          x_new := (x_old + x) / 2;
          y_new := (y_old + y) / 2;
          (p1_x,p1_y,p2_x,p2_y) := to_bezier (p0_x,p0_y,x,y) with (x_old,y_old);          
          b.poly_spline_w1 (p1_x,p1_y) w2 (p2_x,p2_y) to (x_new,y_new);          
          p0_x := x_new;
          p0_y := y_new;
        };
      };
    };
    
    (glyph.x_coord = NULL).if {
      glyph.print;
    };
    
    new_contour := TRUE;
    (glyph.x_coord.lower).to (glyph.x_coord.upper) do { i:INTEGER;            
      x := to_x (glyph.x_coord.item i);
      y := to_y (glyph.y_coord.item i);
      in := glyph.flags.item i & 1 != 0;
      
      (is_debug).if {
        (in).if {
          b.rectangle_fill (x-2,y-2) to (x+2,y+2) color (b.green);
        } else {
          b.rectangle_fill (x-2,y-2) to (x+2,y+2) color (b.red);
        }; 
      };
      
      (new_contour).if {        
        x_contour := x;
        y_contour := y;        
        new_contour := FALSE;
        (is_debug).if {
          b.rectangle (x-4,y-4) to (x+4,y+4) color (b.red);
        } else {
          b.poly_move_to (x,y);
        };
      } else {
        (in).if {
          in_step.value;
        } else {
          out_step.value;
        };        
      };
      
      x_old := x;
      y_old := y;      
      
      (i = glyph.end_pts_of_contours.item n_contour).if {
        x := x_contour;
        y := y_contour;
        in_step.value;
        new_contour := TRUE;
        n_contour := n_contour + 1;
      };
    };
    (is_debug).if_false {
      b.poly_trace_color (b.black);
    };
  );
