/***************************************************************************
*                      Isaac Object Operating System                       *
*                             Isaac Library                                *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/
section HEADER
  
  + name        := ITM_GROUP;
  
  - comment     := "Item Plateform.";
  
  - category    := MICRO;
  
  - bibliography:= "http://IsaacOS.com";
  
  - author      := "Benoit Sonntag (bsonntag@loria.fr)";
  
section INHERIT
  
  * parent_gui_item:GUI_ITEM;
  
section PUBLIC
  
  + operator:CHARACTER;
  
  + itm_left:GUI_ITEM;
  
  + itm_right:GUI_ITEM;
  
  //
  // Creation
  //
  
  - create left:GUI_ITEM and right:GUI_ITEM with op:CHARACTER :SELF <-
  ( + result:SELF;
    
    result := clone;
    result.make left and right with op;
    result
  );
  
  - make left:GUI_ITEM and right:GUI_ITEM with op:CHARACTER <-
  (
    itm_left  := left;
    itm_right := right;
    operator  := op;
    (operator = '|').if {
      width_min  := left.width_min + right.width_min;
      height_min := left.height_min.max (right.height_min);
    } else {
      width_min  := left.width_min.max (right.width_min);
      height_min := left.height_min + right.height_min;      
    };
  );
  
  //
  // 
  // 

  - fix_frame_in f:AREA at x,y:INTEGER size w,h:INTEGER <-
  (    
    (operator = '|').if {
      itm_left .fix_position_in f at x,y 
      size (itm_left.width_min),h;
      
      itm_right.fix_position_in f at (x+itm_left.width_min),y 
      size (w-itm_left.width_min),h;
    } else {
      itm_left .fix_position_in f at x,y                  
      size w,(itm_left.height_min);
      
      itm_right.fix_position_in f at x,(y+itm_left.height_min) 
      size w,(h-itm_left.height_min);
    };
  );
    
section GUI_ITEM
  
  - get_item x,y:INTEGER :GUI_ITEM <-
  ( + result:GUI_ITEM;
        
    (y != 0).if {
      (operator = '/').if {
	result := itm_right.get_item x,(y-1);
      };
    } else {
      (operator = '/').if {
	result := itm_left.get_item x,0;
      } else {
	(x != 0).if {
	  (operator = '|').if {
	    result := itm_right.get_item (x-1),0;
	  };
	} else {
	  (operator = '|').if {
	    result := itm_left.get_item 0,0;
	  };
	};
      };
    };
    result
  );
  
  - get_height_line y:INTEGER :INTEGER <-
  ( + result:INTEGER;
        
    (y != 0).if {
      (operator = '/').if {
	result := itm_right.get_height_line (y-1);
      };
    } else {
      (operator = '/').if {
	result := itm_left.get_height_line 0;
      } else {	
	(operator = '|').if {
	  result := height_min;
	};	
      };
    };
    result
  );