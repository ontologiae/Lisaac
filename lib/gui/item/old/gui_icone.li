/***************************************************************************
*                      Isaac Object Operating System                       *
*                             Isaac Library                                *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Jerome BOUTET  - boutet@loria.fr                       *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/
/*
Copyright (C) 2005  Benoit Sonntag

This library is free software; you can redistribute it and/or
modify it under the terms of the GNU Lesser General Public
License as published by the Free Software Foundation; either
version 2.1 of the License, or (at your option) any later version.

This library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public
License along with this library; if not, write to the Free Software
Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
*/
section HEADER
  
  + name        := GUI_ICONE;
  
  - comment     := "Button plateform.";
  
  - category    := MICRO;
  
  - bibliography:= "http://IsaacOS.com";
  
  - author      := "Benoit Sonntag (bsonntag@loria.fr)";
  
section INHERIT
  
  * parent_item:ITEM;
    
section PUBLIC  
  
  + icone:BITMAP;
  
  //
  // Creation.
  //
  
  - create_in f:AREA at x,y:INTEGER filename txt:ABSTRACT_STRING action a:INBOX :SELF <-
  ( + result:SELF;
        
    result:=clone;
    result.make_in f at x,y filename txt action a;
    
    result
  );
  
  - make_in f:AREA at x,y:INTEGER filename bmp:ABSTRACT_STRING action a:INBOX <-
  ( + entry:ENTRY;
    + bmp_file:BMP_FILE;
    + w,h:INTEGER;
    
    // Read picture file.
    //entry := FILE_SYSTEM.get bmp;
    // BSBS: DEBUT MERDE
    entry := ENTRY.get_entry bmp;
    // BSBS
    ((entry = NULL) || {! entry.is_file}).if {
      VIDEO.message ("ERROR: File '"+bmp+"' not found.\n");
      die_with_code exit_failure_code;
    };
    bmp_file := entry.open_bmp;
    bmp_file.read_header;
    (! bmp_file.is_valid_bmp).if {
      VIDEO.message "Error: BMP format invalid.\n";
      die_with_code exit_failure_code;      
    };
    w := bmp_file.header.width;
    h := bmp_file.header.height;
    icone := BITMAP_24.create w,h;
    bmp_file.fill_bitmap icone;    
    // Close file.
    bmp_file.close;
    // Creation window.
    make_in f at x,y size (w+2),(h+2) label bmp action a;
  );
  
  //
  // Action.
  // 
  
  - receive msg:EVENT <-
  (
    receive_on msg;
  );
  
  //
  // Display.
  //
  
  - draw x0,y0:INTEGER to x1,y1:INTEGER <-
  (     
    clipping x0,y0 to x1,y1;
    
    (((stat & 1) = 0) || {stat = -1}).if {
      // Out      
      rectangle 0,0 to x_max,y_max color color_back;
    } else {
      // In
      ((stat & 2) = 0).if {
	// Off
	color color_light;
	move_to 0,y_max;
	line_v_until 0;
	line_h_until x_max;
	color color_dark;
	line_v_until y_max;
	line_h_until 0;
      } else {
	// On
	color color_dark;
	move_to 0,y_max;
	line_v_until 0;
	line_h_until x_max;
	color color_light;
	line_v_until y_max;
	line_h_until 0;	
      };
    };
    put_bitmap icone to 1,1;    
    (stat = -1).if {
      line 0,0 to x_max,y_max color color_back;
    };
  );
  





