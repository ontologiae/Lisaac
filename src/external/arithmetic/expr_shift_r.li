Section Header

  + name        := EXPR_SHIFT_R;

  - copyright   := "2003-2007 Benoit Sonntag";


  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "Add Binary Expression.";

Section Inherit

  + parent_expr_binary:Expanded EXPR_BINARY;

Section Public

  + symbol:STRING_CONSTANT := ">>";

  //
  // Execute.
  //

  - exec_conservator_right right_cst:INTEGER_CST :EXPR <-
  //-- E >>  0 -> E
  ( + result:EXPR;

    (right_cst.value = 0).if {
      result := left;
      right_cst.remove;
    };
    result
  );

  - exec left_cst:INTEGER_CST and right_cst:INTEGER_CST :EXPR <-
  //-- C1 >> C2  -> C3
  //-- C1 >> -C2 -> Error.
  (
    (right_cst.value < 0).if {
      warning_error (position,"Right shift count is negative.");
      left_cst.set_value 0;
    } else {
      left_cst.set_value (left_cst.value >> right_cst.value);
    };
    right_cst.remove;
    left_cst
  );

  - exec:EXPR <-
  //-- E(unsigned) >> E(unsigned) -> 0
  ( + result:EXPR;
    /*
    (left ~= right) && {left.is_unsigned_type}.if {
      result := INTEGER_CST.create position value 0 type static_type;
      left .remove;
      right.remove;
    };
    */
    result
  );

  - exec_left  left_cst:INTEGER_CST :EXPR <-
  //-- 0 >> E            -> 0
  //-- -1(signed) >> E   -> -1(signed)
  ( + result:EXPR;

    (left_cst.value = 0).if {
      result := left_cst;
      right.remove;
    }.elseif {left_cst.value = -1} then {
      result := left_cst;
      right.remove;
    };
    result
  );

  - exec_right right_cst:INTEGER_CST :EXPR <-
  //-- E >> -C2 -> Error.
  ( + result:EXPR;

    (right_cst.value < 0).if {
      warning_error (position,"Right shift count is negative.");
      right_cst.set_value 0;
      right_cst.cast_type static_type;
      result := right_cst;
      left.remove;
    };
    result
  );




