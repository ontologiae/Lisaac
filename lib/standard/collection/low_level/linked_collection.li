Section Header

  + name    := LINKED_COLLECTION(V);


  - copyright   := "2003-2005 JÃ©rome Boutet, 2003-2007 Benoit Sonntag";

  - comment := "Common root of LINKED_LIST and TWO_WAY_LINKED_LIST..";

Section Inherit

  - parent_collection:COLLECTION(V) := COLLECTION(V);

Section Public

  - lower:INTEGER := 1;
  // Lower index bound is frozen.

  + upper:INTEGER;
  // Memorized upper index bound.

  - create:SELF <-
  ( + result:SELF;

    result := clone;
    result.make;
    result
  );

  - make <-
  // Make an empty list
  (
    deferred;
  );

  - remove_head n:INTEGER <-
  ( + i:INTEGER;

    i := n;
    {i = 0}.until_do {
      remove_first;
      i := i - 1;
    };
  );

  - remove_tail n:INTEGER <-
  ( + i:INTEGER;

    i := n;
    {i = 0}.until_do {
      remove_last;
      i := i - 1;
    };
  );

  - first_index_of element:V :INTEGER <-
  (
    index_of element start lower
  );

  - fast_first_index_of element:V :INTEGER <-
  (
    fast_index_of element start lower
  );

  //
  // Implement manifest generic creation.
  //

  - manifest_make needed_capacity:INTEGER <-
  // Manifest creation of a list of items of type E.
  (
    create
  );

  - manifest_put index:INTEGER to element:V <-
  (
    add_last element;
  );