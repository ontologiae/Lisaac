Section Header

  + name        := EXPR_ADD;

  - copyright   := "2003-2007 Benoit Sonntag";


  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "Add Binary Expression.";

Section Inherit

  + parent_expr_binary:Expanded EXPR_BINARY;

Section Public

  + symbol:STRING_CONSTANT := "+";

  //
  // Execute.
  //

  - exec_conservator_left  left_cst :INTEGER_CST :EXPR <-
  //-- 0 + E -> E
  ( + result:EXPR;

    (left_cst.value = 0).if {
      result := right;
      left_cst.remove;
    };
    result
  );

  - exec_conservator_right right_cst:INTEGER_CST :EXPR <-
  //-- E + 0 -> E
  ( + result:EXPR;

    (right_cst.value = 0).if {
      result := left;
      right_cst.remove;
    };
    result
  );

  - exec left_cst:INTEGER_CST and right_cst:INTEGER_CST :EXPR <-
  //-- C1 + C2 -> C3
  (
    left_cst.set_value (left_cst.value + right_cst.value);
    right_cst.remove;
    left_cst
  );













