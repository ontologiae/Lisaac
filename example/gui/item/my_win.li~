/***************************************************************************
*                      Isaac Object Operating System                       *
*                             Lisaac Compiler                              *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
***************************************************************************/
section HEADER
  
  + name        := MY_WIN;
  
  - category    := MICRO;
  
  - bibliography:= "http://IsaacOS.com";
  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "The first GUI program.";
  
section INHERIT
  
  * parent_inbox:INBOX;
  
section PUBLIC  
  
  - exit:ITEM;
  
  - file:ITEM;    
  
  - menu:GUI_MENU;
  
  - receive msg:EVENT <-
  (
    (msg.source = file).if {
      menu.open_by file;
    };      
    (msg.source = exit).if {
      VIDEO.close;
      die_with_code exit_success_code;
    };
  );
  
  - make <-
  ( 
    
    GUI_RAW.create_in INTERFACE at 10,10 action self with
    (LAB_OUT.create "LAB_OUT object");
    
    GUI_RAW.create_in INTERFACE at 10,30 action self with 
    (
      LAB_BMP.create "../save.bmp" + 
      LAB_OUT.create " (LAB_BMP + LAB_OUT) object"
    );
    
    GUI_RAW.create_in INTERFACE at 10,60 action self with 
    (LAB_OUT.create "LAB_OUT object\nIt's a multiple lines text\nLeft justify.");
    GUI_RAW.create_in INTERFACE at 210,60 action self with
    (LAB_OUT.create "LAB_OUT object\nIt's a multiple lines text\nRight justify." 
    justify (LAB_OUT.right));
    GUI_RAW.create_in INTERFACE at 410,60 action self with
    (LAB_OUT.create "LAB_OUT object\nIt's a multiple lines text\nCenter justify" 
    justify (LAB_OUT.center));
    
    GUI_RAW.create_in INTERFACE at 10,120 action self with
    (
      LAB_IN.create_width 80 +
      LAB_OUT.create " (LAB_IN + LAB_OUT) object"
    );
    
    GUI_RAW.create_in INTERFACE at 10,150 action self with
    (
      LAB_OUT.create "(LAB_OUT + LAB_KEY) object " +
      LAB_KEY.create (KEYBOARD.key_ctrl + KEYBOARD.key_of 'S')
    );
        
    INTERFACE.run;
  );















