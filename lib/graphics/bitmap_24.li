/***************************************************************************
*                      Isaac Object Operating System                       *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Jerome BOUTET  - boutet@loria.fr                       *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/

section HEADER
  
  + name        := BITMAP_24;
  
  - comment     := "Bitmap 24 bits";
  
  - category    := MICRO;
  
  - bibliography:= "http://IsaacOS.com";
  - author      := "Benoit Sonntag (bsonntag@loria.fr), Jerome Boutet (boutet@loria.fr)";
  
section INHERIT
  
  * parent_bitmap_generic:BITMAP;
  
section PUBLIC
  
  - pixel_geometry:PIXEL := PIXEL_24;
  
section BITMAP  
  
  //
  // Data.
  //
  
  // Mapping memory bitmap.
  + image:FIXED_ARRAY[BMP_LINE];
  
  - get_line y:INTEGER :BMP_LINE <-
  (
    image.item y
  );
  
section PUBLIC
  
  //
  // Creation. 
  //
  
  - make w,h:NUMERIC <-
  (
    width  := w;
    height := h;
    //bytes_per_line:=(w * PIXEL_24.size + 7)>>3;
    image := FIXED_ARRAY[BMP_LINE].create h;
    0.to (image.upper) do { y:INTEGER;
      image.put (BMP_LINE_24.create w) to y;
    };
    clipping_off;
  );  
  
  - make w,h:NUMERIC at offset_begin:UINTEGER bytes_per_line lx:NUMERIC <-
  ( + offset:UINTEGER;
    
    width  := w;
    height := h;    
    image := FIXED_ARRAY[BMP_LINE].create h;
    offset:=offset_begin;
    0.to (image.upper) do { y:INTEGER;
      image.put (BMP_LINE_24.create w at offset) to y;
      offset:=offset+lx;
    };
    clipping_off;
  );
  
 

