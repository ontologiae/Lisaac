/***************************************************************************
*                             Lisaac Library                               *
*                      (c) INRIA 2003 see 'licence.txt' for more details   *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Jerome BOUTET  - boutet@loria.fr                       *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/
section HEADER
  
  * name        := TIME;
  
  - category    := MICRO;
  
  - bibliography:= "http://IsaacOS.com";

  - author      := "Benoit Sonntag (bsonntag@loria.fr), Jerome Boutet (boutet@loria.fr)";  

  - comment     := "Time";
  
  - type        := `unsigned long`;
  
  - default     := `0`:TIME;
section INHERIT
  
  - parent_object:OBJECT := OBJECT;
  
section PRIVATE
  
  - to_raw:UINTEGER <-
  (
    UINTEGER.force_conversion self
  );
section PUBLIC

  - hour:USMALLINT <-
  (
    (to_raw >> 24).to_usmallint
  );
  
  - minute:USMALLINT <-
  (
    ((to_raw & 0FF0000h) >> 16).to_usmallint
  );
  
  - second:USMALLINT <-
  (
    ((to_raw & 0FF00h) >> 8).to_usmallint
  );

  - msecond:USMALLINT <-
  (
    (to_raw & 0FFh).to_usmallint
  );
  
section PUBLIC
  
  - create h,m,s,ms:USMALLINT :TIME <-
  ( + t:UINTEGER;
    
    t := h.to_uinteger <<24;
    t := t | (m.to_uinteger << 16);
    t := t | (s.to_uinteger << 8);
    t := t | ms;
    force_conversion t
  );

  - print <-
  (
    (hour < 10).if {
      '0'.print;
    };    
    hour.print;
    ':'.print;
    (minute < 10).if {
      '0'.print;
    };
    minute.print;
    ':'.print;
    (second < 10).if {
      '0'.print;
    };
    second.print;
    '.'.print;
    (msecond < 10).if {
      '0'.print;
    };
    msecond.print;
  );

