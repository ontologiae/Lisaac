Section Header

  + name        := CHARACTER_CST;

  - comment     := "Character constant";

Section Inherit

  + parent_constant:Expanded CONSTANT;

Section Public

  //
  // Value.
  //

  + text:STRING_ALIAS;

  //
  // Creation.
  //

  - create p:POSITION char car:STRING_ALIAS :SELF<-
  ( + result:SELF;
    result := clone;
    result.make p char car;
    result
  );

  - make p:POSITION char car:STRING_ALIAS <-
  (
    position := p;
    text := car;
    static_type := type_character.default;
  );

  - my_copy:SELF <- SELF.create position char text;

  //
  // Comparaison.
  //

  - Self:SELF '~=' Right 60 other:EXPR :BOOLEAN <-
  ( + s:SELF;
    s ?= other;
    (s != NULL) && {text = s.text}
  );

  //
  // Generation.
  //

  - genere buffer:STRING_BUFFER <-
  (
    buffer.add_last '\'';
    buffer.append text;
    buffer.add_last '\'';
  );

  //
  // Display.
  //

  - display buffer:STRING_BUFFER <-
  (
    buffer.add_last '\'';
    buffer.append text;
    buffer.add_last '\'';
    display_ref buffer;
  );





