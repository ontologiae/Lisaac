Section Header

  + name        := TT_TABLE_DIRENTRY;
  - comment     :="The 'Table dir' is followed by 'numtables' table dir entries.";

  - bibliography:="http://IsaacOS.com";
  - author      :="Boutet Jerome (boutet@loria.fr)";

Section Inherit

  - parent:OBJECT := OBJECT;

Section Mapping

  + name1:CHARACTER;      // table type
  + name2:CHARACTER;
  + name3:CHARACTER;
  + name4:CHARACTER;
  + checksum:UINTEGER_32;   // table checksum
  + offset:UINTEGER_32;     // table file offset
  + length:UINTEGER_32;     // table length

Section Public

  - to_big_endian <-
  (   
    checksum := PROCESSOR.to_motorola_uint checksum;
    offset := PROCESSOR.to_motorola_uint offset;
    length := PROCESSOR.to_motorola_uint length;
  );

  //
  // Type
  //

  // Required Tables

  - is_cmap:BOOLEAN <- cmp_with "cmap";

  - is_glyf:BOOLEAN <- cmp_with "glyf";

  - is_head:BOOLEAN <- cmp_with "head";

  - is_hhea:BOOLEAN <- cmp_with "hhea";

  - is_hmtx:BOOLEAN <- cmp_with "hmtx";

  - is_loca:BOOLEAN <- cmp_with "loca";

  - is_maxp:BOOLEAN <- cmp_with "maxp";

  - is_name:BOOLEAN <- cmp_with "name";

  - is_post:BOOLEAN <- cmp_with "post";
  
  - is_os2:BOOLEAN <- cmp_with "OS/2";

  // Optional Tables

  - is_cvt:BOOLEAN <- cmp_with "cvt ";

  - is_fpgm:BOOLEAN <- cmp_with "fpgm";

  - is_hdmx:BOOLEAN <- cmp_with "hdmx";

  - is_kern:BOOLEAN <- cmp_with "kern";

  - is_prep:BOOLEAN <- cmp_with "prep";
  
Section Private
  
  - cmp_with n:ABSTRACT_STRING :BOOLEAN <- CONVERT(SELF,NATIVE_ARRAY(CHARACTER)).on Self.fast_memcmp (n.to_external) until 4;
  
Section Public

  //
  // Display
  //

  - print <-
  (
    "\t Name: ".print;
    name1.print; name2.print; 
    name3.print; name4.print; 
    "\t Checksum: 0x".print;
    checksum.to_hexadecimal.print;
    "\t Off: 0x".print;
    offset.to_hexadecimal.print;
    "\t Length: ".print;
    length.print;
    '\n'.print;
  );

