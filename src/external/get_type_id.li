Section Header

  + name        := GET_TYPE_ID;

  - copyright   := "2003-2007 Benoit Sonntag";


  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "Get intern ID object";

Section Inherit

  + parent_expr:Expanded EXPR;

Section Public

  - is_invariant:BOOLEAN <- TRUE;

  + receiver:TYPE_FULL;

  - static_type:TYPE_FULL <- type_integer.default;

  - get_type t:TYPES_TMP <-
  (
    t.add type_integer;
  );

  //
  // Creation.
  //

  - create p:POSITION receiver e:TYPE_FULL :SELF <-
  ( + result:SELF;

    result := clone;
    result.make p receiver e;
    result
  );

  - make p:POSITION receiver e:TYPE_FULL <-
  (
    position := p;
    receiver := e;
  );

  - my_copy:SELF <- SELF.create position receiver receiver;
  // BSBS: Tu peux renvoyer Self ! Non ?

  //
  // Remove
  //

  - remove <-
  (
    // Nothing.
  );

  //
  // Execute
  //

  - execute_unlink:INSTR <-
  (
    NULL
  );

  - execute_link:EXPR <-
  (
    Self
  );

  //
  // Genere.
  //

  - genere buffer:STRING <-
  (
    (receiver.is_late_binding).if {
      buffer.append "-1";
    } else {
      buffer.append (ALIAS_STR.separate);
      buffer.append (receiver.raw.intern_name);
      buffer.append (ALIAS_STR.separate); // <=> "__";
    };
  );

  //
  // Display.
  //

  - display buffer:STRING <-
  (
    buffer.append "type_id(";
    receiver.display buffer;
    buffer.add_last ')';
  );









