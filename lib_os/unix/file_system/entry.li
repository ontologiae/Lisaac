/***************************************************************************
*                      Isaac Object Operating System                       *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Jerome BOUTET  - boutet@loria.fr                       *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/
section HEADER
  
  + name        := ENTRY;
  
  - category    := MICRO;
  
  - bibliography:= "http://IsaacOS.com";

  - author      := "Benoit Sonntag (bsonntag@loria.fr), Jerome Boutet (boutet@loria.fr)";  

  - comment     := "Entry ANSI";
    
section INHERIT
  
  * parent_abstract_entry:ABSTRACT_ENTRY;
    
section PUBLIC  
    
  //
  // Physical implementation.
  //

  - physical_make:BOOLEAN <-
  ( + pe:NATIVE_ARRAY[CHARACTER];
    + tt:POINTER;
    + result:BOOLEAN;

    pe := path.to_external;
    `{ struct stat t`;
      result := `lstat(@pe,&t)`:INTEGER = 0;
      (result).if {
	is_directory := `S_ISDIR(t.st_mode)`:BOOLEAN(TRUE,FALSE);
	tt := `localtime(&(t.st_atime))`:POINTER;
	access_date := to_date tt;
	access_time := to_time tt;
	tt := `localtime(&(t.st_mtime))`:POINTER;
	update_date := to_date tt;
	update_time := to_time tt;
	size := `t.st_size`:UINTEGER;
      };
    `}`;
    result
  );
  
  - physical_remove_directory:BOOLEAN <-
  ( + p:NATIVE_ARRAY[CHARACTER];
    p := path.to_external;
    `rmdir(@p)`:(INTEGER) = 0
  );
  
  - physical_remove_file:BOOLEAN <-
  ( + p:NATIVE_ARRAY[CHARACTER];
    p := path.to_external;
    `remove(@p)`:(INTEGER) = 0
  );
  
  - physical_rename old_path:ABSTRACT_STRING with new_path:ABSTRACT_STRING :BOOLEAN <-
  ( + old_p,new_p:NATIVE_ARRAY[CHARACTER];
    old_p := old_path.to_external;
    new_p := new_path.to_external;
    `rename(@old_p,@new_p)`:(INTEGER) = 0
  );
  
  //
  // Time / Date: Unix -> Lisaac
  //
  
  - to_date t:POINTER :DATE <-
  ( + result:DATE;
    + wd,md,m:USMALLINT;
    + y:USHORTINT;
    
    y  := `((struct tm *)@t)->tm_year`:USHORTINT + 1900;
    m  := `((struct tm *)@t)->tm_mon` :USMALLINT + 1;
    md := `((struct tm *)@t)->tm_mday`:USMALLINT;
    wd := `((struct tm *)@t)->tm_wday`:USMALLINT;
    (! wd.in_range 1 to 7).if { // Bug in UNIX ?
      wd := 1;
    };
    result := DATE.create y,m,md,wd     
  );

  - to_time t:POINTER :TIME <-
  (
    TIME.create 
    (`((struct tm *)@t)->tm_hour`:USMALLINT),
    (`((struct tm *)@t)->tm_min` :USMALLINT),
    (`((struct tm *)@t)->tm_sec` :USMALLINT),
    0
  );
  
