Section Header

  + name        := ITM_TYPE_STYLE;

  - copyright   := "2003-2007 Benoit Sonntag";


  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "Type with style";

Section Inherit

  + parent_itm_type_simple:Expanded ITM_TYPE_SIMPLE;

Section Private

  - dico_ts:FAST_ARRAY(ITM_TYPE_STYLE) := FAST_ARRAY(ITM_TYPE_STYLE).create_with_capacity 32;

  - create n:STRING_CONSTANT style s:STRING_CONSTANT :SELF <-
  ( + result:SELF;

    result := clone;
    result.make n style s;
    result
  );

  - make n:STRING_CONSTANT style s:STRING_CONSTANT <-
  (
    name  := n;
    style := s;
  );

Section Public

  + style:STRING_CONSTANT;

  - get n:STRING_CONSTANT style s:STRING_CONSTANT :SELF <-
  ( + result:SELF;
    + idx:INTEGER;

    idx := dico_ts.lower;
    {
      (idx <= dico_ts.upper) && {
	(dico_ts.item idx.name  != n) ||
	{dico_ts.item idx.style != s}
      }
    }.while_do {
      idx := idx + 1;
    };
    (idx <= dico_ts.upper).if {
      result ?= dico_ts.item idx;
    } else {
      result := create n style s;
      dico_ts.add_last result;
    };
    result
  );


