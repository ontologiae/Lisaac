///////////////////////////////////////////////////////////////////////////////
//                             Lisaac Compiler                               //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////
Section Header
  
  + name        := ITM_TYPE_GENERIC;

  - copyright   := "2003-2007 Benoit Sonntag";

  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "Generic style type";
  
Section Inherit
  
  + parent_itm_type_style:Expanded ITM_TYPE_STYLE;
  
Section Private
  
  - dico:FAST_ARRAY[ITM_TYPE_GENERIC] := FAST_ARRAY[ITM_TYPE_GENERIC].create_with_capacity 32;

  - create n:STRING_CONSTANT style s:STRING_CONSTANT with lt:FAST_ARRAY[ITM_TYPE_MONO] :SELF <-
  ( + result:SELF;
    
    result := clone;
    result.make n style s with lt;
    result
  );
  
  - make n:STRING_CONSTANT style s:STRING_CONSTANT with lt:FAST_ARRAY[ITM_TYPE_MONO] <-
  (
    name      := n;
    style     := s;
    list_type := lt;
  );
 
Section Public
  
  - hash_code:INTEGER <- name.hash_code; 

  + list_type:FAST_ARRAY[ITM_TYPE_MONO];
  
  - get n:STRING_CONSTANT style s:STRING_CONSTANT 
  with lt:FAST_ARRAY[ITM_TYPE_MONO] :ITM_TYPE_GENERIC <-
  ( + result:ITM_TYPE_GENERIC;
    + idx:INTEGER;
    
    idx := dico.lower;
    {
      (idx <= dico.upper) && {
	(dico.item idx.name      != n ) || 
	{dico.item idx.style     != s } || 
	{dico.item idx.list_type != lt}
      }
    }.while_do {
      idx := idx + 1;
    };    
    (idx <= dico.upper).if {
      result := dico.item idx;
    } else {
      result := create n style s with lt;
      dico.add_last result;
    };
    result
  );
  
  - to_run:TYPE_FULL <-
  ( + lst:FAST_ARRAY[TYPE_FULL];
    + t:TYPE_FULL;
        
    lst := ALIAS_ARRAY[TYPE_FULL].new;
    (list_type.lower).to (list_type.upper) do { j:INTEGER;
      t := list_type.item j.to_run;      
      ? {t != NULL};
      lst.add_last t;
    };
    lst := ALIAS_ARRAY[TYPE_FULL].alias lst;
    TYPE_GENERIC.get Self with lst 
  );
  
  - display buffer:STRING <-
  (
    (style != NULL).if {
      buffer.append style;
      buffer.add_last ' ';
    };
    buffer.append name;
    buffer.add_last '[';
    (list_type.lower).to (list_type.upper - 1) do { j:INTEGER;
      list_type.item j.display buffer;
      buffer.add_last ',';
    };
    list_type.last.display buffer;
    buffer.add_last ']';
  );
  
  //
  // Cast.
  //
  
  - append_cast_name_in buf:STRING <- 
  (
    parent_itm_type_style.append_cast_name_in buf;
    buf.append "_of_";
    (list_type.lower).to (list_type.upper - 1) do { j:INTEGER;
      list_type.item j.append_cast_name_in buf;
      buf.append "_and_";
    };
    list_type.last.append_cast_name_in buf;
  );