Section Header

  + name        := TT_NAME;

  - comment     :="Naming Table.";

  - bibliography:="http://IsaacOS.com";
  - author      :="Boutet Jerome (boutet@loria.fr)";

Section Inherit

  - parent_object:OBJECT := OBJECT;

Section Public

  + header:TT_NAME_HEADER;

  + record:FAST_ARRAY(Expanded TT_NAME_RECORD);

  + data:FAST_ARRAY(FAST_ARRAY(CHARACTER));

  - create file:FILE :SELF <-
  ( + result:SELF;
    ? { file != NULL };
    result := clone;
    result.make file;
    result
  );

  - make file:FILE <-
  ( + cursor:UINTEGER_32;
    + tmp_data:FAST_ARRAY(CHARACTER);
    cursor := file.cursor;
        
    header := TT_NAME_HEADER.clone;
    file.read header;
    header.to_big_endian;

    record := FAST_ARRAY(Expanded TT_NAME_RECORD).create (header.num_name_records);            
    data := FAST_ARRAY(FAST_ARRAY(CHARACTER)).create_with_capacity (header.num_name_records);
    file.read record size (header.num_name_records);

    cursor := cursor + header.storage_offset; 
    0.to (header.num_name_records - 1) do { i:INTEGER;
      // Init of records
      record.item i.to_big_endian;
      // Init of datas
      (record.item i.string_length = 0).if {
        data.add_last NULL;
      } else {
        tmp_data := FAST_ARRAY(CHARACTER).create_with_capacity (record.item i.string_length);
        data.add_last tmp_data;
        file.set_cursor (cursor + record.item i.string_offset);
        file.read tmp_data size (record.item i.string_length);      
      };
    };
  );

  - print <-
  (
    "\n\n********* NAME TABLE *********\n".print;
    header.print;
    0.to (header.num_name_records - 1) do { i:INTEGER;
      "\n     ".print;
      (i+1).print;
      '.'.print;
      record.item i.print;
      "\n       Data: ".print;
      (data.item i = NULL).if {
        "(Nothing)".print;
        } else {
          0.to (data.item i.upper) do { j:INTEGER;
            data.item i.item j.print;
          };
      };
      '\n'.print;
    };
    '\n'.print;
  );
