Section Header

  + name     := GL_EXTENSION;

  - comment  := "OpenGL ARB extensions manager";

Section Inherit

  - parent_object:OBJECT := OBJECT;

Section Public

  - extension_names:STRING;

  - name:STRING_CONSTANT <- STRING_CONSTANT;


  - create :SELF <-
  // dont clone prototype
  (  + result:SELF;
    (SELF.is_supported).if {
      result := SELF;
      result.make;
    };
    result
  );

  - make <- deferred;

  - is_supported:BOOLEAN <-
  (
    + extensions:NATIVE_ARRAY(CHARACTER);
    + i:INTEGER;
    + result:BOOLEAN;

    (extension_names = NULL).if {
      extensions := `glGetString(GL_EXTENSIONS)`:NATIVE_ARRAY(CHARACTER);
      extension_names := STRING.create 256;

      {extensions.item i != '\0'}.while_do {
	// copy names of supported extensions

	extension_names.add_last (extensions.item i);
	i := i+1;
      };
    };
    (extension_names.has_substring name).if {
      result := TRUE;
    };
    result
  );