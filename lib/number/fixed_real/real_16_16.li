Section Header

  + name    := Expanded REAL_16_16;

  - export  := REAL_32, REAL_64;

  - copyright   := "2003-2005 JÃ©rome Boutet, 2003-2007 Benoit Sonntag";

  - comment := "signed 16.16 fixed float.";

  - type    := `signed int`;
  - default := (0.to_raw_real_16_16);

Section Insert

  - parent_signed_fixed_real:SIGNED_FIXED_REAL := SIGNED_FIXED_REAL;

Section SELF

  - shift_bits:INTEGER <- 16;

Section Public

  - round:INTEGER <-
  // Rounded integral value.
  ( + result:INTEGER;
    result := to_raw_integer;
    ((result & 8000h) != 0).if {
      result := (result >> 16) + 1;
    } else {
      result := result >> 16;
    };
    result
  );

  //
  // Range.
  //

  - minimum:INTEGER_64  <- INTEGER_16.minimum;

  - maximum:UINTEGER_64 <- INTEGER_16.maximum;

  //
  // Convertion format with test.
  //

  - to_real_16_16:REAL_16_16 <- Self;

  //
  // Convertion
  //

  - append_in buffer:STRING_BUFFER <-
  // Append in the `buffer' the equivalent of `to_string'. No new STRING_BUFFER
  // creation during the process.
  ( + val:INTEGER;
    + old_count,new_count:INTEGER;
    ? {buffer!=NULL};

    val := to_raw_integer;

    (val < 0).if {
      val := - val;
      buffer.extend '-';
    };
    (val >> 16).append_in buffer;
    buffer.extend '.';

    old_count:=buffer.count;
    //     1/65536 = 0.00390625 => 8 digit.
    (((val & 0FFFFh).to_uinteger_64 * 100000000)>>16).to_integer.append_in buffer;
    new_count:=old_count+8;
    {buffer.count!=new_count}.while_do {
      buffer.insert '0' to old_count;
    };
  );









