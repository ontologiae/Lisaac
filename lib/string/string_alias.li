Section Header

  + name    := Strict STRING_ALIAS;

  - comment := "String built in.";

Section Inherit

  + inherit_abstract_string:Expanded STRING;
  
  //Section ALIAS TO
Section Public
  
  - create_copy other:STRING_BUFFER :SELF <-
  ( + result:SELF;

    result := clone;
    result.make_copy other;
    result
  );

  - make_copy other:STRING_BUFFER <-
  ( + c:INTEGER;

    c := other.upper;
    (c != -1).if {
      storage := NATIVE_ARRAY(CHARACTER).create (c+2);
      storage.copy_from (other.storage) until c;
      storage.put '\0' to (c+1);
      upper := c;
      hash_code := hash_code_intern; 
    };
  );
    
Section Public
  
  + hash_code:INTEGER := ( `init_string_alias()`; 0);
  
  - to_string_buffer:STRING_BUFFER <-
  ( + new:STRING_BUFFER;
    new := STRING_BUFFER.create capacity;
    new.copy Self;
    new
  );
  
  - to_string_alias:STRING_ALIAS <- Self;
  
  //
  // The Guru section: The Compiler consideration.
  //

  - to_external:NATIVE_ARRAY(CHARACTER) <- storage;
  // Gives C access to the internal `storage' (may be dangerous).
  // To be compatible with C, a null character is added at the end
  // of the internal `storage'. This extra null character is not
  // part of the Lisaac STRING.

  - println <-
  [
    -? {storage.item count = '\0'};
  ]
  (
    to_external.println;
  );

  //
  // Debug: Require / Ensure / Check
  //

  - Self:SELF '?'  test:{BOOLEAN} <- test ? Self;

  - Self:SELF '-?' test:{BOOLEAN} <- test -? Self;

  - Self:SELF '+?' test:{BOOLEAN} <- test +? Self;


