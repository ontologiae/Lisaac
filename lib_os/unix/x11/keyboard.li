/***************************************************************************
*                      Isaac Object Operating System                       *
*                             Isaac                                        *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
***************************************************************************/
section HEADER

  - name    := KEYBOARD;
  
  - category:= MICRO;
  
  - bibliography:="http://IsaacOS.com";
  - author      :="Sonntag Benoit (bsonntag@loria.fr)";
  - comment     :="X11 - Keyboard Driver";

  - version :="1.0";  
  - date    :="2003/04";
  
section INHERIT

  * parent_input_keyboard:INPUT_KEYBOARD;

section PRIVATE
  
  // Lower case.
  - lower_case:STRING_CONSTANT := "&é\"'(-è_çà)=\8\\9\azertyuiop^$\13\ qsdfghjklmù  *wxcvbn,;:!";
  // Upper case.
  - upper_case:STRING_CONSTANT := "1234567890°+\8\\9\AZERTYUIOP\"£\13\ QSDFGHJKLM%  µWXCVBN?./§";
  
  // Num lock.
  - num_on :STRING_CONSTANT := "789-456+1230.";
  - num_off:STRING_CONSTANT := "BUA-L\0R+EDZIS";
  
  // AltGr.
  - alt_gr:STRING_CONSTANT := "¹~#{[|`\\^@]}";
  
  // Extension key.
  - ext_key:STRING_CONSTANT := "BUAL REDZIS  HP";
  
  - keyup cu:USMALLINT :USMALLINT <-
  ( + result:USMALLINT;
    cmd := cmd & 0F7h; // Deactivate the cmd bit    
    bin_code.put ((bin_code.item (cu>>3)) & ~(1<<(cu&7))) to (cu>>3);
    // Analyze: for deactivate the cmd
    ((cu == 37) || {cu == 109}).if {
      //CTRL 1 or CTRL2
      cmd := cmd & (~01h);
    }.elseif { cu == 113 } then {
      // Alt Gr
      cmd := cmd & (~02h);
    }.elseif { cu == 64 } then {
      // Alt
      cmd := cmd & (~04h);      
      result := ascii_code;
      ascii_code := 0;      
    }.elseif { (cu == 50) || { cu == 62} } then {
      // Shift 1 or Shift 2
      cmd := cmd & (~10h);
    };
    result
  );

  - keydown cu:USMALLINT :USMALLINT <-
  ( + result:USMALLINT;
    bin_code.put ((bin_code.item (cu>>3)) | (~(1<<(cu&7)))) to (cu>>3); // Activate key
    // Analyze: for activate the cmd
    ((cu == 37) || {cu == 109}).if {
      //CTRL1 or CTRL2
      cmd := cmd | 01h;
    }.elseif { cu == 113 } then {
      // Alt Gr
      cmd := cmd | 02h;
    }.elseif { cu == 64 } then {
      // Alt
      cmd := cmd | 04h;    
    }.elseif { cu == 65 } then {
      // Space
      result := ' '.to_usmallint;      
    }.elseif { (cu == 50) || { cu == 62} } then {
      // Shift 1 or Shift 2
      cmd := cmd | 10h;
      ((cmd & 20h)!=0).if {
	cmd := cmd & (~20h);
	//light;
      };
    }.elseif { cu == 66 } then {
      // Cap
      cmd := cmd ^ 20h;
      //light;
    }.elseif { cu == 77 } then {
      // Num Lock
      cmd := cmd ^ 40h;
    }.elseif { cu == 78 } then {
      // Scroll Lock
      cmd := cmd ^ 80h;
    }.elseif { cu == 9 } then {
      // Esc
      cmd :=  cmd | 08h;
      result := 27;
    }.elseif {((cmd&2)!=0) && {cu.in_range 10 to 21}} then {
      cmd :=  cmd & (~02h);
      result := alt_gr.item (cu-9).to_usmallint;
    }.elseif {cu.in_range 10 to 61} then {
      ((cmd & 30h)==0).if {
	result := lower_case.item (cu-9).to_usmallint;
      } else {
	result := upper_case.item (cu-9).to_usmallint;
      };
      (result <= 13).if {
	cmd := cmd | 08h;
      };	
    }.elseif {cu = 63} then {
      result := '*'.to_usmallint;
    }.elseif {cu.in_range 67 to 76} then {
      cmd :=  cmd | 08h;      // F1 to F10 = cmd
      result :=  cu - 67 + 'a'.to_usmallint;  // 'a' to 'j'
    }.elseif {cu.in_range 79 to 91} then {  
      ((cmd & 40h) = 0).if {
	// Ver num off
	result := num_off.item (cu-78).to_usmallint;
	(result.to_character.in_range 'A' to 'Z').if {
	  cmd := cmd | 8h;
	};
      } else {
	// Verr num on
	result := num_on.item (cu-78).to_usmallint;
	((cmd & 4) != 0).if {
	  ascii_code := ascii_code * 10 + (result-'0'.to_usmallint);
	  result := 0;
	};
      };
    }.elseif {(cu = 95) || {cu = 96}} then { 
      cmd :=  cmd | 08h;      // F11 to F12 = cmd
      result :=  cu - 95 + 'k'.to_usmallint;  // 'k' to 'l'
    }.elseif {cu = 94} then {
      ((cmd & 30h)==0).if {
	result := '<'.to_usmallint;
      } else {
	result := '>'.to_usmallint;
      };
    }.elseif {cu.in_range 97 to 111} then {
      cmd := cmd | 08h;
      result := ext_key.item (cu-96).to_usmallint;       
    }.elseif {cu = 112} then {
      result := '/'.to_usmallint;
    };
    result
  );
    




