/***************************************************************************
*                             Lisaac Library                               *
*                      (c) INRIA 2003 see 'licence.txt' for more details   *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Jerome BOUTET  - boutet@loria.fr                       *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/
section HEADER
  
  * name        := DATE;
  
  - category    := MICRO;
  
  - bibliography:= "http://IsaacOS.com";

  - author      := "Benoit Sonntag (bsonntag@loria.fr), Jerome Boutet (boutet@loria.fr)";  

  - comment     := "Date";
  
  - type        := `unsigned long`;
  
  - default     := `0`:DATE;
  
section INHERIT
  
  - parent_object:OBJECT := OBJECT; 
  
section PRIVATE
  
  - to_raw:UINTEGER <-
  (
    UINTEGER.force_conversion self
  );
  
section PUBLIC
  
  - '>' right 60 other:SELF :BOOLEAN <- to_raw > other.to_raw;
  
  - '<' right 60 other:SELF :BOOLEAN <- to_raw < other.to_raw;
  
  - '>=' right 60 other:SELF :BOOLEAN <- to_raw >= other.to_raw;
  
  - '<=' right 60 other:SELF :BOOLEAN <- to_raw <= other.to_raw;
  
  - year :USHORTINT <-
  ( 
    (to_raw >> 16).to_ushortint
  );
  
  - month:USMALLINT <-
  (
    ((to_raw & 0FF00h)>>8).to_usmallint 
  );
  
  - day  :USMALLINT <-
  (
    (to_raw & 01Fh).to_usmallint 
  );
  
  - week_day :USMALLINT <-
  (
    ((to_raw >> 5) & 0111b).to_usmallint
  );
  
  
section PUBLIC
  
  - create y:USHORTINT,m,d,wd:USMALLINT :DATE <-
  ( + date:UINTEGER;
    ? { m.in_range 1 to 12};
    ? { d.in_range 1 to 31};
    ? { wd.in_range 1 to 7};
    date := y.to_uinteger << 16;
    date := date | (m.to_uinteger << 8);
    date := date | d;
    date := date | (wd << 5);
    force_conversion date
  );
  
  - to_string:STRING <-
  ( + result:STRING;
    result := STRING.create 13;
    append_in result;
    result
  );
  
  - append_in str:STRING <-
  (
    week_day
    .when 1 then {
      str.append "Mo";
    }
    .when 2 then {
      str.append "Tu";
    }
    .when 3 then {
      str.append "We";
    }
    .when 4 then {
      str.append "Th";
    }
    .when 5 then {
      str.append "Fr";
    }
    .when 6 then {
      str.append "Sa";
    }
    .when 7 then {
      str.append "Su";
    };
    (week_day != 0).if {
      str.add_last ' ';
    };
    day.append_in str format 2 with '0';
    str.add_last '/';
    month.append_in str format 2 with '0';
    str.add_last '/';
    year.append_in str format 4;           
  );

  - append_short_in str:STRING <-
  (
    day.append_in str format 2 with '0';
    str.add_last '/';
    month.append_in str format 2 with '0';
    str.add_last '/';
    (year % 100).append_in str format 2 with '0';           
  );

  - append_short2_in str:STRING <-
  (
    day.append_in str format 2 with '0';
    str.add_last '/';
    month.append_in str format 2 with '0';
    str.add_last '/';
    year.append_in str format 4 with '0';           
  );
    
  - print <-
  (
    to_string.print;
  );


