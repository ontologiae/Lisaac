/***************************************************************************
*                      Isaac Object Operating System                       *
*                             Isaac Library                                *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/
section HEADER
  
  + name        := GUI_LABEL;
  
  - comment     := "Label Plateform.";
  
  - category    := MICRO;
  
  - bibliography:= "http://IsaacOS.com";
  
  - author      := "Benoit Sonntag (bsonntag@loria.fr)";
  
section INHERIT
  
  - parent_inbox:INBOX := INBOX;
  
section PUBLIC
    
  + name:ABSTRACT_STRING;
  
  + next:GUI_LABEL;  

  - '+' right 80 other:GUI_LABEL :GUI_LABEL <-
  (
    next := other;
    self
  );
  
  //
  // Size.
  //
  
  - width_min :INTEGER <- 
  ( + result:INTEGER;
    result := my_width_min;
    (next = NULL).if_false {      
      result := result + next.width_min;
    };
    result
  );
  
  - height_min:INTEGER <- 
  ( + result:INTEGER;
    result := my_height_min;
    (next = NULL).if_false {
      result := result.max (next.height_min);
    };
    result
  );
  
  //
  // Event.
  //

  - receive msg:EVENT <-
  (
  //  receive_nothing msg;
  );
    
  //
  // Display.
  //
  
  - display_in bmp:GUI_ITEM to x0,y0:INTEGER <- 
  ( + h,ih,iw:INTEGER;
    + l:GUI_LABEL;
    + col:UINTEGER;
    + y1,ix0,iy0,ix1,iy1:INTEGER;
    h := height_min;
    l := self;
    ix0 := x0;
    y1  := y0+h-1;
    col := bmp.rgbcolor;    
    {      
      ih := l.my_height_min;
      iw := l.my_width_min;
      //
      iy0 := y0 + ((h - ih) >> 1);
      iy1 := iy0 + ih - 1;
      ix1 := ix0 + iw - 1;
      bmp.color (bmp.rgbbackcolor);
      (iy0 > y0).if {
	bmp.rectangle_fill ix0,y0 to ix1,(iy0-1);
      };
      (y1 > iy1).if {
	bmp.rectangle_fill ix0,(iy1+1) to ix1,y1;
      };
      bmp.color col;
      l.my_display_in bmp to ix0,iy0;
      ix0 := ix1 + 1;
      l := l.next;
    }.do_until {l = NULL};
  );
  
section GUI_LABEL
  
  - my_width_min :INTEGER <- deferred;
  
  - my_height_min:INTEGER <- deferred;
  
  - my_display_in bmp:GUI_ITEM to x,y:INTEGER <- deferred;
  