Section Header

  + name        := PROTOTYPE_CST;

  - copyright   := "2003-2007 Benoit Sonntag";


  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "Prototype constant";

Section Inherit

  + parent_constant:Expanded CONSTANT;

Section Public

  //
  // Creation.
  //

  - create p:POSITION type t:TYPE_FULL :SELF<-
  ( + result:SELF;
    ? {p.code != 0};
    ? {t != NULL};

    result := clone;
    result.make p type t;
    result
  );

  - make p:POSITION type t:TYPE_FULL <-
  (
    position    := p;
    static_type := t; //.to_strict;
  );

  - my_copy:SELF <-
  (
    SELF.create position type static_type
  );

  //
  // Comparaison.
  //

  - Self:SELF '~=' Right 60 other:EXPR :BOOLEAN <-
  ( + p:PROTOTYPE_CST;
    p ?= other;
    (p != NULL) && {static_type = p.static_type}
  );

  //
  // Execute
  //

  - execute_link:EXPR <-
  (
    Self
  );

  //
  // Generation.
  //

  - genere buffer:STRING <-
  (
    static_type.genere_value buffer;
  );

  //
  // Display.
  //

  - display buffer:STRING <-
  (
    static_type.display buffer;
    display_ref buffer;
  );


