Section Header

  + name    := ITM_WRITE;

  - comment := "Parent for all write";

Section Inherit

  + parent_itm_code:Expanded ITM_CODE;

Section Public

  //
  // Data
  //

  + assign:ITM_CODE;

  + value:ITM_CODE;

  - type:STRING_ALIAS <-
  (
    deferred;
    NULL
  );

  //
  // Constructor
  //

  - create p:POSITION assign n:ITM_CODE with v:ITM_CODE :SELF <-
  [ -? {p != 0}; ]
  ( + result:SELF;
    result := clone;
    result.make p assign n with v;
    result
  );

  - make p:POSITION assign n:ITM_CODE with v:ITM_CODE <-
  (
    position := p;
    assign   := n;
    value    := v;
  );

  //
  // Access.
  //

  - get_simple_name:STRING_ALIAS <-
  ( + result:STRING_ALIAS;
    + without_arg:ITM_READ;

    without_arg ?= assign;
    (without_arg != NULL).if {
      result := without_arg.name;
    } else {
      semantic_error (position,"ITM_WRITE: Not yet implemented.");
    };
    result
  );

  //
  // Display.
  //

  - append_in buffer:STRING_BUFFER <-
  (
    assign.append_in buffer;
    buffer.append type;
    value.append_in buffer;
  );

Section Private

  - affect name:STRING_ALIAS with v:EXPR :EXPR <-
  ( + loc:LOCAL;
    + result:EXPR;

    loc := lookup name;
    (loc != NULL).if {
      result := affect_local loc with v;
    } else {
      result := affect_slot name with v;
    };
    result
  );

  - affect_local loc:LOCAL with v:EXPR :EXPR <-
  ( + e:INSTR;
    + result:EXPR;
    + val:EXPR;

    (loc.style = '-').if {
      result := affect_slot (loc.intern_name) with v;
    } else {
      (loc.style = ' ').if {
	POSITION.put_error semantic text "Argument assignment is not possible.";
	loc.position.put_position;
	position.put_position;
	POSITION.send_error;
      };
      val := v.check_type (loc.type) with position;
      e := loc.write position value val;
      list_current.add_last e;
      result := loc.read position;
    };
    result
  );

  - affect_slot name:STRING_ALIAS with v:EXPR :EXPR <-
  ( + loc:VARIABLE;
    + slo:ITM_SLOT;    
    + node:NODE;
    + result:EXPR;
    + rec:EXPR;
    + typ:TYPE;
    + em:EXPR_MULTIPLE;
    + new_val:EXPR;
    + lst:ARRAY EXPR;
    + types:ARRAY TYPE_FULL;
    + type:TYPE_FULL;
    + itm_tm:ITM_TYPE_MULTI;

    loc := lookup "Self";
    rec := loc.read position;
    //
    typ := rec.static_type.raw;
    slo := typ.get_itm_slot name;
    (slo = NULL).if {
      string_tmp.copy "Slot `";
      string_tmp.append name;
      string_tmp.append "' not found in static type ";
      typ.append_name_in string_tmp;
      string_tmp.add_last '.';
      semantic_error (position,string_tmp);
    };
    // Control type.    
    em ?= v;
    (em != NULL).if {
      itm_tm ?= slo.result_type;
      types := ALIAS_ARRAY TYPE_FULL.new;
      itm_mu.to_run_in types for profil_slot;
      //
      lst := em.expr_list;
      (lst.lower).to (lst.upper - 1) do { j:INTEGER;
        new_val := (lst.item j).check_type (types.item j) with (slot.position);
        lst.put new_val to j;
      };
      new_val := (lst.last).check_type (types.last) with (slot.position);
      lst.put new_val to (lst.upper);
      new_val := em;
      //
      ALIAS_ARRAY TYPE_FULL.free types;
    } else {
      type := slo.result_type.to_run_for profil_slot;
      new_val := v.check_type type with (slo.position);
    };
    //
    node := NODE.new_write position slot slo receiver rec value new_val;
    list_current.add_last node;
    result := node.result_expr;
    result
  );

