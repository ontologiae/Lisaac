/***************************************************************************
*                      Isaac Object Operating System                       *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Jerome BOUTET  - boutet@loria.fr                       *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/
section HEADER
  
  + name        := ENTRY;
  
  - category    := MICRO;
  
  - bibliography:= "http://IsaacOS.com";

  - author      := "Benoit Sonntag (bsonntag@loria.fr), Jerome Boutet (boutet@loria.fr)";  

  - comment     := "Entry ANSI";
  
  - external := 
`
SYSTEMTIME systime;
WIN32_FIND_DATA data2;
HANDLE hfile2;
`;
  
section INHERIT
  
  * parent_abstract_entry:ABSTRACT_ENTRY;
    
section PUBLIC  
    
  //
  // Physical implementation.
  //

  - physical_make:BOOLEAN <-
  ( + pe:NATIVE_ARRAY[CHARACTER];
    + result:BOOLEAN;
    
    pe := path.to_external;
    `hfile2 = FindFirstFile(@pe,&data2)`;
    result := `hfile2 != INVALID_HANDLE_VALUE`:BOOLEAN(TRUE,FALSE);
    (result).if {
      is_directory := `(data2.dwFileAttributes & FILE_ATTRIBUTE_DIRECTORY) != 0`:BOOLEAN(TRUE,FALSE);
      `FileTimeToSystemTime(&data2.ftLastAccessTime,&systime)`;
      access_date := to_date;
      access_time := to_time;
      `FileTimeToSystemTime(&data2.ftLastWriteTime,&systime)`;
      update_date := to_date;
      update_time := to_time;
      size := `(data2.nFileSizeHigh << 16)|data2.nFileSizeLow`:UINTEGER;
      `FindClose(hfile2)`;
    };
    result
  );
  
  - physical_remove_directory:BOOLEAN <-
  ( + p:NATIVE_ARRAY[CHARACTER];
    p := path.to_external;
    `rmdir(@p)`:(INTEGER) = 0
  );
  
  - physical_remove_file:BOOLEAN <-
  ( + p:NATIVE_ARRAY[CHARACTER];
    p := path.to_external;
    `remove(@p)`:(INTEGER) = 0
  );
  
  - physical_rename old_path:ABSTRACT_STRING with new_path:ABSTRACT_STRING :BOOLEAN <-
  ( + old_p,new_p:NATIVE_ARRAY[CHARACTER];
    old_p := old_path.to_external;
    new_p := new_path.to_external;
    `rename(@old_p,@new_p)`:(INTEGER) = 0
  );
  
section PRIVATE
  
  - to_date:DATE <-
  ( + y:USHORTINT;
    + m,d,wd:USMALLINT;
    
    y  := `systime.wYear`:USHORTINT;
    m  := `systime.wMonth`:USMALLINT;
    d  := `systime.wDay`:USMALLINT;
    wd := `systime.wDayOfWeek`:USMALLINT;
    DATE.create y,m,d,wd
  );
  
  - to_time:TIME <-
  ( + h,m,s,cs:USMALLINT;
    
    h  := `systime.wHour`:USMALLINT;
    m  := `systime.wMinute`:USMALLINT;
    s  := `systime.wSecond`:USMALLINT;
    cs := `systime.wMilliseconds/10`:USMALLINT;
    TIME.create h,m,s,cs 
  );