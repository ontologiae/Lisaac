Section Header

  + name     := FS_MIN;

  - comment  := "File system minimum";

  - external := 
`
#undef  __wur
#define __wur
#include <unistd.h>
`;

Section Inherit

  - parent_object:OBJECT <- OBJECT;

Section Private

  - string_tmp:STRING_BUFFER := STRING_BUFFER.create 128;

Section Public

  //
  // Independance File System
  //

  - open_read n:STRING :POINTER <-
  ( + buf:NATIVE_ARRAY(CHARACTER);
    
    string_tmp.copy n;
    buf := string_tmp.to_external;
    `fopen((char*)@buf,"rb")`:(POINTER)
  );

  - open_write n:STRING :POINTER <-
  ( + buf:NATIVE_ARRAY(CHARACTER);
    string_tmp.copy n;
    buf := string_tmp.to_external;
    `fopen((char*)@buf,"wb")`:(POINTER)
  );
  
  - read_line f:POINTER in buf:STRING_BUFFER :BOOLEAN <-
  ( + ptr:NATIVE_ARRAY(CHARACTER);
    + result:POINTER;
    + sz:INTEGER;
    sz := buf.capacity;
    ptr := buf.to_external;
    result := `fgets((void *)(@ptr),@sz,(FILE*)(@f))`:(POINTER);
 //   ptr.put '\0' to sz;
    buf.from_external ptr;
    result != NULL
  );

  - read f:POINTER in buf:STRING_BUFFER size sz:INTEGER :INTEGER <-
  [
    ? {buf.capacity > sz};
  ]
  ( + ptr:NATIVE_ARRAY(CHARACTER);
    + result:INTEGER;

    ptr := buf.to_external;
    result := `fread((void *)(@ptr),(size_t)(1), (size_t)(@sz),(FILE*)(@f))`:(INTEGER);
    ptr.put '\0' to sz;
    buf.from_external ptr;
    result
  );

  - write f:POINTER with buf:STRING_BUFFER size sz:INTEGER :INTEGER <-
  [
    ? {sz <= buf.count};
  ]
  ( + ptr:NATIVE_ARRAY(CHARACTER);
    + result:INTEGER;
    
    ptr := buf.to_external;
    result := `fwrite((void *)(@ptr),(size_t)(1), (size_t)(@sz),(FILE*)(@f))`:(INTEGER);
    result
  );

  - close p:POINTER <- `fclose((FILE*)(@p))`;

  - file_size p:POINTER :INTEGER <-
  ( + result:INTEGER;
    `fseek((FILE*)(@p),0,SEEK_END)`;
    result := `ftell((FILE *)@p)`:INTEGER;
    `fseek((FILE*)(@p),0,SEEK_SET)`;
    result
  );

  - make_file new_path:STRING :BOOLEAN <-
  ( + p:NATIVE_ARRAY(CHARACTER);
    + stream:POINTER;
    + result:BOOLEAN;

    string_tmp.copy new_path;
    p := string_tmp.to_external;
    stream := `fopen((char*)@p,"w+b")`:POINTER;
    (result := (stream != NULL)).if {
      close stream;
    };
    result
  );

