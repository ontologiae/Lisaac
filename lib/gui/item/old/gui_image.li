/***************************************************************************
*                      Isaac Object Operating System                       *
*                             Isaac Library                                *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Jerome BOUTET  - boutet@loria.fr                       *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/
section HEADER
  
  + name        := GUI_IMAGE;
  
  - comment     := "Just the picture.";
  
  - category    := MICRO;
  
  - bibliography:= "http://IsaacOS.com";
  
  - author      := "Benoit Sonntag (bsonntag@loria.fr)";
  
section INHERIT
  
  * parent_item:ITEM;
    
section PUBLIC  
  
  + icone:BITMAP;
  
  //
  // Creation.
  //
  
  - create_in f:AREA at x,y:INTEGER filename txt:ABSTRACT_STRING :SELF <-
  ( + result:SELF;
        
    result:=clone;
    result.make_in f at x,y filename txt;
    
    result
  );
  
  - make_in f:AREA at x,y:INTEGER filename bmp:ABSTRACT_STRING <-
  ( + entry:ENTRY;
    + bmp_file:BMP_FILE;
    + w,h:INTEGER;
    
    // Read picture file.
    //entry := FILE_SYSTEM.get bmp;
    // BSBS: DEBUT MERDE
    entry := ENTRY.get_entry bmp;
    // BSBS
    ((entry = NULL) || {! entry.is_file}).if {
      VIDEO.message ("ERROR: File '"+bmp+"' not found.\n");
      die_with_code exit_failure_code;
    };
    bmp_file := entry.open_bmp;
    bmp_file.read_header;
    (! bmp_file.is_valid_bmp).if {
      VIDEO.message "Error: BMP format invalid.\n";
      die_with_code exit_failure_code;      
    };
    w := bmp_file.header.width;
    h := bmp_file.header.height;
    icone := BITMAP_24.create w,h;
    bmp_file.fill_bitmap icone;    
    // Close file.
    bmp_file.close;
    // Creation window.
    make_in f at x,y size w,h label bmp action NULL;
  );
  
  //
  // Action.
  // 
  
  - receive msg:EVENT <-
  ( 
    receive_nothing msg;
  );
  
  //
  // Display.
  //
  
  - draw x0,y0:INTEGER to x1,y1:INTEGER <-
  (     
    clipping x0,y0 to x1,y1;
    put_bitmap icone to 0,0;    
  );
  





