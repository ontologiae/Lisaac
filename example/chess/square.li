Section Header
  
  + name     := Expanded SQUARE;
  
  - comment   := "3D Chess Game";
  
  - author    := "Damien Bouvarel (dams.bouvarel@wanadoo.fr)";
  
  - comment     := "square position on chessboard";
  
  - type        := `unsigned int`;
  - default     := ( CONVERT(INTEGER,SQUARE).on 0 );
  
Section Insert
  
  - parent_object:OBJECT := OBJECT;
  
Section Public
  
  - value:UINTEGER_32 <- CONVERT(SQUARE,UINTEGER_32).on Self;
  
  //
  // Creation.
  //
  
  - create (x,y:INTEGER) :SQUARE <-
  ( + cod:UINTEGER_32;
    ? {x.in_range 0 to 7};
    ? {y.in_range 0 to 7};
    
    cod := y*8 + x;
    CONVERT(UINTEGER_32,SQUARE).on cod
  );
  
  //
  // Content
  //
  
  - piece:PIECE <- 
  (
    CHESS.chessboard.item value
  );
  
  - set_piece p:PIECE <- 
  (
    CHESS.chessboard.put p to value;
  );
  
  - clear <-
  (
    set_piece NULL;
  );
  
  
  - position:(INTEGER,INTEGER) <-
  (
    (value % 8, value / 8)
  );
  
  - x:INTEGER <- value % 8;
  - y:INTEGER <- value / 8;
  
  
  - render r:RENDERER color col:COLOR <-
  ( + x,y,sz:INTEGER;
    
    (x,y) := position;
    sz := CHESS.square_size/2;
    
    ((x < 8) && {y < 8}).if {
      r.transform.new_matrix {
        
        r.transform.translatef (3*CHESS.square_size+CHESS.square_size/2, 0, -3*CHESS.square_size-CHESS.square_size/2);
        r.transform.translatef (-x*CHESS.square_size, 0, y*CHESS.square_size);
        
        r.texture2d.disable;    
        r.color_buffer.set_color col;
       
        r.blending.apply (r.blending.one,r.blending.zero);// erase color
        r.blending.enable; 
        
        r.vb.new_quads {  
          r.vb.add_texel2f (0.0, 1.0);
          r.vb.add_vertex3f (-sz, 0.1, sz);
          
          r.vb.add_texel2f (0.0, 0.0);
          r.vb.add_vertex3f (-sz, 0.1, -sz);
          
          r.vb.add_texel2f (1.0, 0.0);
          r.vb.add_vertex3f (sz, 0.1, -sz);
          
          r.vb.add_texel2f (1.0, 1.0);
          r.vb.add_vertex3f (sz, 0.1, sz);
        };
        r.blending.disable;
        r.color_buffer.set_color3f (1,1,1);
      };
    };
  );
  
  