/***************************************************************************
*                      Isaac Object Operating System                       *
*                                Isaac Library                             *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
***************************************************************************/
section HEADER

  + name    := EVENT_MOUSE;
  
  - category:=MICRO;
  
  - bibliography:="http://IsaacOS.com";
  - author      :="Sonntag Benoit (bsonntag@loria.fr)";
  - comment     :="Mouse event.";
  
section INHERIT

  * parent_event:EVENT;
  
section PUBLIC
  
  - source:INBOX := MOUSE;
    
  + x_current:INTEGER;
  + y_current:INTEGER;
    
  + right:BOOLEAN;
  + left:BOOLEAN;
  
  + prev:EVENT_MOUSE;
  
  - set_prev new_prev:EVENT_MOUSE <-
  (
    prev := new_prev;
  );
  
  - make xn,yn:INTEGER button l,r:BOOLEAN <-
  (
    x_current:=xn;
    y_current:=yn;
    left :=l;
    right:=r;
    destination := NULL;
  );
  
  - copy_from evt:EVENT_MOUSE <-
  (
    ? {evt != NULL};
    x_current := evt.x_current;
    y_current := evt.y_current;
    left := evt.left;
    right := evt.right;
    destination := NULL;
  );
   
  - dx:INTEGER <- (x_current - prev.x_current);
  
  - dy:INTEGER <- (y_current - prev.y_current);
  
  - left_up:BOOLEAN    <- ((! prev.left) && {left});

  - left_down:BOOLEAN  <- (prev.left && {! left});
  
  - right_up:BOOLEAN   <- ((! prev.right) && {right});
  
  - right_down:BOOLEAN <- (prev.right && {! right});
    
  - is_pressed:BOOLEAN <- ((left!=prev.left) || {right!=prev.right});
  
  - is_moving:BOOLEAN  <- ((x_current!=prev.x_current) || {y_current!=prev.y_current});

  - is_moving_only:BOOLEAN <- ((! is_pressed) && {is_moving});
  
  //
  // Window consideration.
  //
  
  - window:AREA <- 
  ( + result:AREA;
    result ?= destination;
    ? {result != NULL};
    result
  );
  
  - x_relative:INTEGER <-
  ( 
    x_current - window.x_window    
  );
  
  - y_relative:INTEGER <-
  ( 
    y_current - window.y_window
  );
  
  - is_in:BOOLEAN <-
  ( 
    INTERFACE.get_object x_current,y_current = destination
  );
  
  - in_up:BOOLEAN <- (! prev.is_in) && {is_in};
  
  - is_out:BOOLEAN <- ! is_in;
  
  - out_up:BOOLEAN <- (prev.is_in) && {! is_in};
  
  //
  // Display.
  //  

  - display <-
  (
    string_msg.copy "Mouse event";
    left.if {
      string_msg.append " Left";
    };
    right.if {
      string_msg.append " Right";
    };
    string_msg.add_last '\n';
    VIDEO.message string_msg;
  );
  
  