/****************************************************************************
*                             Lisaac Library                               *
*                      (c) INRIA 2003 see 'licence.txt' for more details   *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Jerome BOUTET  - boutet@loria.fr                       *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/

section HEADER
  
  + name        := ITERATOR_ON_TWO_WAY_LINKED_LIST[E];
  - comment     :=" .";
  
  - category    := MICRO;
  
  - bibliography:="http://IsaacOS.com";
  - author      :="Boutet Jerome (boutet@loria.fr)";
  
section INHERIT
  
  - parent_iterator:ITERATOR[E] := ITERATOR[E];
  
section PUBLIC
  
  - linked_list:TWO_WAY_LINKED_LIST[E];
  // The one to be traversed.

  - item_link:LINK2[E];
  //  Memorize the current position.

  
  - create twll: TWO_WAY_LINKED_LIST[E] :SELF <-
  (
    + result:SELF;
    result := clone;
    result.make twll
  );

  - make twll:TWO_WAY_LINKED_LIST[E] <-
  (
    ? { twll != NULL };
    
    linked_list := twll;
    item_link := linked_list.first_link;
    
    ? { linked_list = twll };
  );

  - start <-
  (
    item_link := linked_list.first_link;
  );

  - is_off:BOOLEAN <-
  (
    item_link = NULL
  );

  - item:E <-
  (
    item_link.item
  );

  - next <-
  (
    item_link := item_link.next;
  );

