/***************************************************************************
*                      Isaac Object Operating System                       *
*                             Isaac Library                                *
*                      LORIA - UHP - INRIA - FRANCE                        *
*                   Jerome BOUTET  - boutet@loria.fr                       *
*                   Benoit SONNTAG - bsonntag@loria.fr                     *
*                          http://www.IsaacOS.com                          *
****************************************************************************/

section HEADER
  
  + name        := PIXEL_16;
  
  - comment     := "";
  
  - category    := MICRO;
  
  - bibliography:= "http://IsaacOS.com";
  
  - author      := "Benoit Sonntag (bsonntag@loria.fr), Jerome Boutet (boutet@loria.fr)";
  
section INHERIT
  
  - parent_pixel:PIXEL := PIXEL;
  
section MAPPING
  
  + real_color:USHORTINT;
  
section PRIVATE  
  
  - convert_5_to_8:FIXED_ARRAY[USMALLINT] :=
  ( + result:FIXED_ARRAY[USMALLINT];
    + step,value:UREAL_26_6;
    
    result:=FIXED_ARRAY[USMALLINT].create 32;
    step := 255;
    step := step /# 31;
    0.to 31 do { j:INTEGER;
      result.put (value.to_usmallint) to j;
      value := value + step;
    };
    result
  );
  
  - convert_6_to_8:FIXED_ARRAY[USMALLINT] :=
  ( + result:FIXED_ARRAY[USMALLINT];
    + step,value:UREAL_26_6;
    
    result:=FIXED_ARRAY[USMALLINT].create 64;
    step := 255;
    step := step /# 63;
    0.to 63 do { j:INTEGER;
      result.put (value.to_usmallint) to j;
      value := value + step;
    };
    result
  );
      
section PUBLIC
    
  - red:USMALLINT <- 
  (
    (((real_color >> 11) & 1Fh)  << 3).to_usmallint
    //convert_5_to_8.item (real_color >> 11)
  );
  
  - green:USMALLINT <- 
  (
    (((real_color >> 5) & 3Fh) << 2).to_usmallint
    //convert_6_to_8.item ((real_color >> 5) & 03Fh)
  );

  - blue:USMALLINT <- 
  (
    ((real_color & 1Fh) << 3).to_usmallint
//    convert_5_to_8.item (real_color & 01Fh)
  );
  
  - color col:UINTEGER :UINTEGER <-
  (     
    real_color := (
      ((col & 0F80000h) >> 8) |
      ((col & 000FC00h) >> 5) |
      ((col & 00000F8h) >> 3)
    ).to_ushortint
  );
  
  - color_rgb r,g,b:USMALLINT <-
  ( 
    real_color := ((r & 0F8h).to_ushortint << 8) |
                  ((g & 0FCh).to_ushortint << 3) |
		  ((b & 0F8h).to_ushortint >> 3);
  );
  
  - size:USMALLINT       := 16; 
  - red_size:USMALLINT   := 5;
  - red_pos:USMALLINT    := 11;
  - green_size:USMALLINT := 6;
  - green_pos:USMALLINT  := 5;
  - blue_size:USMALLINT  := 5;
  - blue_pos:USMALLINT   := 0;
  - reserved_size:USMALLINT :=0;
  - reserved_pos:USMALLINT  :=0;
    
  - copy other:SELF <-  
  (
    real_color := other.real_color;
  );
  
  - to_pixel_16:PIXEL_16 <- self;




