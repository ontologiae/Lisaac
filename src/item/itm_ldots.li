Section Header

  + name        := ITM_LDOTS;

  - copyright   := "2003-2007 Benoit Sonntag";


  - author      := "Sonntag Benoit (bsonntag@loria.fr)";
  - comment     := "`...' for inherit contract";

Section Inherit

  + parent_itm_constant:Expanded ITM_CODE;

Section Public

  //
  // Constructor
  //

  - create p:POSITION :SELF <-
  ( + result:SELF;
    result := clone;
    result.make p;
    result
  );

  - make p:POSITION <-
  (
    position := p;
  );

  //
  // Runnable
  //

  - to_run:INSTR <-
  ( + contract:ITM_LIST;
    + result:INSTR;
    + slot_code:SLOT_CODE;

    slot_code ?= profil_slot.slot;
    contract := slot_code.previous_contract;
    (contract != NULL).if {
      result := contract.to_run_expr;
    } else {
      result := NOP;
    };
    result
  );

  - to_run_expr:EXPR <-
  (
    warning_error (position,"ITM_LDOTS.to_run_expr");
    crash;
    NULL
  );

  //
  // Display.
  //

  - append_in buffer:STRING <-
  (
    buffer.append "...";
  );
